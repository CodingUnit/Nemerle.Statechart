using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using System.IO;
using CommonLib.Macros;

namespace Nemerle.Statechart
{
  class FsmBuilder
  {
    parser : Parser;
    ty : TypeBuilder;
    top : TopDeclaration;
    locator : Locator;
    typer : Typer;
    
    public this(ty : TypeBuilder, typer : Typer, top : TopDeclaration, fsm : StateNode.StateMachine)
    {
      this(ty, typer, top);
      this.fsm = fsm;
      parser = Parser();
      locator = Locator(parser, loc, this);
    }

    public this(ty : TypeBuilder, typer : Typer, top : TopDeclaration)
    {
      this.ty = ty;
      this.top = top;
      this.typer = typer;
      parser = Parser();
      locator = Locator(parser, loc, this);
    }

    text : string;
    
    public this(name : string, text : string)
    {
      this.text = text;
      this.name = name;
      parser = Parser();
      locator = Locator(parser, loc, this);
    }
    
    [Accessor]
    mutable fsm : StateNode.StateMachine;

    AttributesAndMods : AttributesAndModifiers
    {
      get
      {        
        top?.ParsedAttributsAndModifiers
      }
    }

    [Memoized]
    attribute : option[PExpr]
    {
      get
      {
        def find_func(x)
        {
          | <[ statechart(..$x) ]> => match (x)
                                      {
                                        | x :: _ => Some(x)
                                        | _      => None()
                                      }
          | _                      => None()
        }
        
        if (AttributesAndMods != null)
        {
          AttributesAndMods.ParsedCustomAttributes.FindMap(find_func)
        }
        else None()
      }
    }

    loc : Location
    {
      get
      {
        if (attribute is Some(a)) a.Location else top?.Location;
      }
    }

    [Memoized]
    public body : string
    {
      get
      {
        match (attribute)
        {
          | Some(<[ $(str : string) ]>) => str
          | _                           => null
        }
      }
    }

    env : GlobalEnv
    {
      get
      {
        ty.GlobalEnv
      }
    }

    attr_loc : Location
    {
      get
      {
        top.ParsedAttributsAndModifiers.Location
      }
    }
    
    public ConvLocation(location : Nitra.Location) : Location
    {
  //    def nloc = Nitra.Util.ToNemerleLocation(location);
      def startLineColumn = location.StartLineColumn;
	    def startLine = startLineColumn.Line;
	    def startCol = startLineColumn.Column;
	    def endLineColumn = location.EndLineColumn;
	    def endLine = endLineColumn.Line;
	    def endCol = endLineColumn.Column;
	    def nloc =  Location(location.Source.File.FullName, startLine, startCol, endLine, endCol);
	    def nloc = if (nloc.Line > 1) Location(nloc, nloc.Line + loc.Line - 1, nloc.Column, loc.Line + nloc.EndLine - 1, nloc.EndColumn) 
	               else Location(nloc, nloc.Line, nloc.Column + loc.Column, nloc.EndLine, nloc.EndColumn + loc.Column);
      //def nloc = if (nloc.Line > 1) Location(nloc, nloc.Line - 1 + nloc.Line, nloc.Column) else Location(nloc, nloc.Line, nloc.Column + loc.Column);
      nloc
    }    
    
    ParseInternal(file : string, text : string) : StateNode.StateMachine
    {
      def (ast, err) = parser.Parse(loc, text);
      if (ast == null)
      {
        foreach (err in err)
        {
          def nloc = ConvLocation(err.Location);
          def line = nloc.Line;
          def col  = nloc.Column;
          Message.Error(nloc, $"Error in input at $(line):$(col): $(err.Text) of file: $file");
        }
        null
      } else ast |> OpenIncludes
    }
    
    public Parse(text : string) : void
    {
      fsm = if (!text.IsEmpty()) 
             {
               //when (loc.File == "AlarmClockInternals.n") assert2(false);
               //assert2(false);
               ParseInternal(loc.File, text)
             } else null;
       when (fsm != null) locator.Locate(fsm);
    }
    
    OpenIncludes(src : StateNode.StateMachine) : StateNode.StateMachine
    {
      def included = List();
      
      def open(s, ch)
      {
        match (s)
        {
          | StateNode.State(include = (_ :: _) as i) as st => 
              def fsms = i.Select(_.file).Map(x => File.ReadAllText(x) |> ParseInternal(x, _));
              fsms |> included.AddRange;
              def st = st.ChangeSubStates(ch.Map(x => x :> StateNode.State));
              fsms.Fold(st, (x, a) => StateNode.Combine(a, x.state));
          | StateNode.StateMachine as st       => st.ChangeState(ch.First() :> StateNode.State)
          | _                                  => s
        }
      }
      
      if (src != null)
      {
        def res = src.Reduce(open) :> StateNode.StateMachine;
        included.Fold(res, (x, a) => a.AddTopDecls(x));
      } else null
    }
    
    public Parse() : void
    {
      parser.flags_map = Hashtable(FsmAnalyser.Flags);      
      //assert2(false);
      def text = if (ty != null)
      {
        parser.env = env;
        CheckBody();
        def text = body ?? "";
        //def name = Path.GetFileNameWithoutExtension(loc.File);
        //when (!text.IsEmpty())
        //  File.WriteAllText($"$(name)_gen.nsch", text);
        text
      } else
      {
        def compiler = CompilerHost();
        parser.env = compiler.CoreEnv;
        text
      }
      Parse(text);
    }

    public static GetParsedResult(ty : TypeBuilder) : FsmBuilder
    {
      def data = ty.Manager.UserData;
      if (data.Contains(key)) 
      {
        def data = data[key] :> data_type;
        if (data.Find((x, _) => x == ty.Name) is Some((_, d))) d else null
      } else null
    }

    public static key = "statechart";
    type data_type = list[string * FsmBuilder];

    static Memoize(ty : TypeBuilder, tree : FsmBuilder) : void
    {
      def storage = ty.Manager.UserData;
      def name = ty.Name;
      def data = if (storage.Contains(key))
                  {
                    storage[key] :> data_type;
                  } else [];
      def data = (name, tree) :: data;
      storage[key] = data
    }
    
    public CheckEventClasses(typer : Typer) : void
    {
      def base_type = typer.BindFixedType(<[ HsmEvent ]>);
      def evt = fsm.event_classes;
      foreach (StateNode.EventClassDecl(name = n) as e in evt)
      {
        def parts = n.Parts;
        match (ty.GlobalEnv.LookupType(parts))
        {
          | Some(ty) => unless (ty.IsDerivedFrom(base_type.TypeInfo)) Message.Error(e.Location, $<#Event class "$(ty.FullName)" must implement HsmEvent class#>);
          | _        => ()
        }
      }
    }
    
    name : string;
    
    Name : string
    {
      get
      {
        if (ty != null) ty.Name else name
      }
    }
    
    public GetEventClasses(evt : list[StateNode.EventClassDecl]) : list[EventClass]
    {
      def find(e)
      {
        def name = e.name;
        def evts = e.events;
        def parts = name.Parts;
        match (ty.GlobalEnv.LookupType(parts))
        {
          | Some(ty) => def nested = ty.GetNestedTypes();
                        
                        def nested = if (evts.IsEmpty()) nested else 
                        {
                          def evts = evts.Map(x => if (nested.Find(y => y.Name == x.ToUpperFirst()) is Some(y)) (x, y) else (x, null));
                          foreach ((ev, null) in evts)
                          {
                            Message.Error(e.Location, $<#Event "$ev" not found in event class "$(name.FullName)"#>);
                          }
                          evts.Map((_, x) => x)
                        }
                        nested.Map(x => EventClass.External(x.FullName.SplitToList('.'), x))
          | _        => Message.Error(e.Location, $<#Event class "$(name.FullName)" not found#>);
                        []
        }
      }
      
      evt.SelectMany(find).NToList()
    }
    
    public CheckBody() : void
    {
      when (attribute is Some() && body == null) Message.Error(attr_loc, "Body expression must be a string");
    }
    
    public BuildGraph(other : list[FsmBuilder]) : void
    {
      when (fsm.flags.Logging) Console.WriteLine($"Starting analyse fsm $(Name)...");
      when (fsm.flags.Debug) assert2(false);
      //def mach = ty.Manager.UserData[key] :> data_type;
      def evt_class = GetEventClasses(fsm.event_classes);
      def mach = other.Map(x => (x.fsm.name, x.fsm));
      def analyser = FsmAnalyser(fsm, Name, mach, evt_class);
      analyser.CreateGraph();
      def warn = analyser.WarningMessages;
      when (fsm.flags.Logging) Console.WriteLine($"Finish analyse with $(analyser.ErrorMessages.Count()) errors and $(warn.Count()) warnings");
      def warn_file = $"$(Name)Warnings.log";
      if (!warn.IsEmpty()) 
      {
        def text = warn.Map((_, x) => x);
        warn.Iter(Message.Warning);
        when (fsm.flags.Debug) File.WriteAllLines(warn_file, text);
      } else when (fsm.flags.Debug) File.Delete(warn_file);
      def err_file = $"$(Name)errors.log";
      if (analyser.Error)
      {
        def err = analyser.ErrorMessages;
        def text = err.Map((_, x) => x);
        when (fsm.flags.Debug) System.IO.File.WriteAllLines(err_file, text);
        err.Iter(Message.Error)
      } else
      {
        when (fsm.flags.Debug) File.Delete(err_file)
      }
    }
    
    public Build() : void
    {
      when (fsm.flags.Logging) Console.WriteLine($"Starting analyse fsm $(ty.Name)...");
      when (fsm.flags.Debug) assert2(false);
      def mach = ty.Manager.UserData[key] :> data_type;
      def evt_class = GetEventClasses(fsm.event_classes);
      def analyser = FsmAnalyser(fsm, ty.Name, mach.Map((x, y) => (x, y.fsm)), evt_class);
      analyser.Analyse();
      def warn = analyser.WarningMessages;
      when (fsm.flags.Logging) Console.WriteLine($"Finish analyse with $(analyser.ErrorMessages.Count()) errors and $(warn.Count()) warnings");
      def warn_file = $"$(ty.Name)Warnings.log";
      if (!warn.IsEmpty()) 
      {
        def text = warn.Map((_, x) => x);
        warn.Iter(Message.Warning);
        when (fsm.flags.Debug) File.WriteAllLines(warn_file, text);
      } else when (fsm.flags.Debug) File.Delete(warn_file);
      def err_file = $"$(ty.Name)errors.log";
      if (analyser.Error)
      {
        def err = analyser.ErrorMessages;
        def text = err.Map((_, x) => x);
        when (fsm.flags.Debug) System.IO.File.WriteAllLines(err_file, text);
        err.Iter(Message.Error)
      } else
      {
        when (fsm.flags.Debug) File.Delete(err_file);

        when (!fsm.flags.OnlyAnalyse) 
        {
          if (analyser.Complete)
          {
            when (fsm.flags.Logging) Console.WriteLine($"Starting generate fsm $(ty.Name)...");
            def active = analyser.ActiveStates.Where(_.Used).NToList();
            def gen = StaticGenerator(active, analyser.FsmFlags, analyser.InitialTransition, analyser.StatesId, analyser.LoadedMachines, ty);
            gen.Generate();
            when (fsm.flags.Logging) Console.WriteLine($"Finish generating $(ty.Name)");
            ty.UserData[FsmBuilder.key] = true;
          } else Message.Warning("No Statechart to generate")
        }
      }

    }

    
    
    public static ParseAll(ty : TypeBuilder, typer : Typer) : void
    {
      def types = ty.Manager.Hierarchy.TopTypeBuilders();
      foreach (ty in types)
      {
        def types = ty.AstParts;
        def builders = types.Map(x => FsmBuilder(ty, typer, x));
        builders.Iter(x => x.Parse());
        def parsed = builders.Filter(x => x.fsm != null);
        match (parsed)
        {
          | []  => ()
          | [p] => Memoize(ty, p)
          | lst => def fsm = MergePartial(lst.Map(_.Fsm));
                   Memoize(ty, FsmBuilder(ty, typer, lst.Head.top, fsm))
        }
      }
    }
      
    static MergePartial(fsm : list[StateNode.StateMachine]) : StateNode.StateMachine
    {
      fsm.Reduce(StateNode.Combine)
    }

  }

}
