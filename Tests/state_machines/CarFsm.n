using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Statechart;

namespace Nemerle.Statechart.Tests
{
  /// <summary>
  /// Description of CarFsm.
  /// </summary>
  [statechart(
  <#
  flags : testing;
  
  //flags : debug;
  
  name Car;
  
  0 / initialize() => EngineOff;
  
  state EngineOff
  {
    TURN_KEY [isBatteryOk()] / start() => MotorOn;
  }
  
  state MotorOn
  {
    0 / Oil-Display() => Stand;
    
    TURN_KEY / stop() => EngineOff;
    entry / RadioOn();
    exit  / RadioOff();
    do    / coolEngine();
    
    state Stand
    {
      GAS_PEDAL / accelerate() => Drive;
    }
    
    state Drive
    {
      BREAK_PEDAL [speed == 1] / Break() => Stand; // v == 1
      BREAK_PEDAL [speed > 1]  / Break() => @;     // v > 1
      GAS_PEDAL / accelerate();
    }
    
  }
  
  
  
  #>
  )]
  public class CarFsm
  {
    battery_level : int;
    speed : double;
    
    initialize() : void
    {
    }
    
    isBatteryOk() : bool
    {
      battery_level != 0
    }
    
    SpeedAtLow : bool
    {
      get
      {
        speed == 1
      }
    }
    
    SpeedAboveLow : bool
    {
      get
      {
        speed > 1
      }
    }
  }
  
}
