using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Statechart;
using System.Threading;

namespace Nemerle.Statechart.Tests
{
  /// <summary>
  /// Description of AlarmClock.
  /// </summary>
  [statechart(
  <#
  flags auto_initial testing force_concurrent_combination;
  //flags debug;
  
  state Operations : Powered
  {
  }
  
  state Run : Operations
  {
    entry / set_am_pm;
    after (500 ms) / update_time;

    state DisplayCurrentTime
    {
      entry / disp_cur_time_blink;
      after (1 s) => @;
      push_hour                     => DisplayAlarmTime;
      push_min [in_state(AlarmOn)]  => Beep;
      
    }
    
    state DisplayAlarmTime
    {
      entry / disp_alarm_time;
      push_hour, push_min, after (5 s) => DisplayCurrentTime;
    }
    
    state Beep
    {
      push_hour, push_min, after (5 s) => DisplayCurrentTime;
      do / Beep;
    }

    time_set  => TimeSet;
    alarm_set => AlarmSet;
  }
  
  state AlarmSet : Operations
  {
    entry / al_icon_on;
    entry / setup_time = alarm;
    exit  / al_icon_off;
    
    run / alarm = setup_time => Run;
    
    state SetupTime
    {
      state SetNormal
      {
        entry / {disp_cur_set_time(); set_am_pm(setup_time);}
        after (300 ms) => @;
        push_hour / UpdateHours;
        push_min  / UpdateMins;
        hold_hour => SetFastHour;
        hold_min  => SetFastMin;
      }
        
      state SetFast
      {
        push_hour / SetHour;
        push_min  / SetMin;
        release_hour, release_min => SetNormal;
            
        state SetFastHour
        {
          do / send_hour_fast;
        }

        state SetFastMin : SetFast
        {
          do / send_min_fast;
        }
      }
    
    }
   
  }
    
  state TimeSet : Operations
  {
    run / set_cur_time => Run;
    entry / setup_time = Now;
    
    state DisplayCurrentTime
    {
      submachine SetupCurrentTime[SetupTime]
      {
        
      }
    }
  }
  #>)]
  partial public class AlarmClock
  {
    
    mutable setup_time : DateTime;
    
    AlarmTime : string
    {
      get
      {
        TimeString(alarm)
      }
    }
    
    SetupTime : string
    {
      get
      {
        TimeString(setup_time)
      }
    }
    
    update_time() : void
    {
      def pass = DateTime.Now - Now + diff;
      def old = Now;
      Now = Now.Add(pass);
      if (old.Hour >= 12 && Now.Hour < 12) pass_12_hours() else
      if (old.Hour < 12 && Now.Hour == 12) after_12_hours() else ();
      when (Now.Hour == alarm.Hour && Now.Minute == alarm.Minute && Now.Second == alarm.Second) current_time_is_alarm();
    }
    
    send_hour_fast(tok : CancellationToken) : void
    {
      while (!tok.IsCancellationRequested)
      {
        this.push_hour();
        Thread.Sleep(100);
      }
    }

    send_min_fast(tok : CancellationToken) : void
    {
      while (!tok.IsCancellationRequested)
      {
        this.push_min();
        Thread.Sleep(100);
      }
    }
    
    set_cur_time() : void
    {
      Now = setup_time;
      diff = Now - DateTime.Now;
    }
    
    disp_setup_time() : void
    {
      OnShow(SetupTime)
    }
    
    disp_alarm_time() : void
    {
      OnShow(AlarmTime)
    }
    
    disp_cur_set_time() : void
    {
      OnShow(if (blink) "     " else TimeString(setup_time))
    }
    
    UpdateHours() : void
    {
      setup_time = setup_time.AddHours(1);
    }
 
    UpdateMins() : void
    {
      def old = setup_time;
      def newt = setup_time.AddMinutes(1);
      setup_time = if (newt.Hour != old.Hour) newt.AddHours(old.Hour - newt.Hour) else newt;
    }
    
    disp_cur_time_blink() : void
    {
      OnShow(CurTime);
    }
    
    TimeString(time : DateTime, blink : bool = false) : string
    {
      time.ToString(if (!blink) "hh:mm" else "hh mm")
    }
    
    CurTime : string
    {
      get
      {
        TimeString(Now, blink)
      }
    }
    
    OnShow(str : string) : void
    {
      Show?.Invoke(str)
    }
    
    SetHour() : void
    {
      UpdateHours();
      disp_setup_time(); 
      set_am_pm(setup_time);
    }
    
    SetMin() : void
    {
      UpdateMins();
      disp_setup_time(); 
      set_am_pm(setup_time);
    }
    
    public event Show : Action[string];
  }
}

