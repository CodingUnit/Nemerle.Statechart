using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler.Parsetree;
using Nemerle.Statechart;
using System.Threading.Tasks;
using System.Collections.Concurrent;
using System.Threading;

namespace Nemerle.Statechart
{
  /// <summary>
  /// Description of GeneratorInit.
  /// </summary>
  public partial class StaticGenerator
  {
    DefineMain() : void
    {
      def def_initial(i)
      {
        DefineTransBody(i.Map(x => x.Value : StateTransition), GenCtx.Normal());
      }

      def initial = def_initial(analyser.TopState.InitialTransitions()); // define initial transition
      def events = analyser.Events |> <[ $_ ]>;
      def states = analyser.LivedNestedStates.Exclude(_.IsTopState()).Map(x => <[ $(x.Name() : usesite) ]>);
      def state_names = $[(st.Name(), $"State$(st)"), st in analyser.LivedNestedStates, st.UsedInGenerate() && !st.IsTopState() && !(st.Data is FsmState.ConcurrentSubRegion)]; // создаем имена состояний      
      def state_init_expr = state_names.Map((x, y) => <[ $(y : usesite) = State.$(x : usesite)();]>);
      def cons_expr = constructor.MapDefault(x  => <[ $(x : usesite)(); ]>, <[ () ]>);
      def trans_complete_expr =  if (analyser.Fsm.flags.TransitionCompleted) <[ OnTransitionCompleted(); ]> else <[ () ]>;
      def dummy =  <[decl:
                  class Test
                  {
                    [Accessor] mutable cur_state : State;

                    // define constructor
                    public this()
                    {
                        // constructor body
                        {..$state_init_expr}
                        {..$init_exprs}
                        $cons_expr;
                    }

                    
                    // define method for changing state
                    TrySwitch(st : State) : void
                    {
                       when (st != null)
                       {
                         cur_state = st;
                         $trans_complete_expr;
                       }
                    }

                    // define method for changing state
                    Switch(st : State) : void
                    {
                        //IsEventDeclined = false;
                        cur_state = st;
                        $trans_complete_expr;
                    }

                    public IsTerminated : bool
                    {
                      get
                      {
                        cur_state == null
                      }
                    }

                    QueueThread() : void
                    {
                      while (!IsTerminated)
                      {
                        mutable item;
                        if (queue.TryDequeue(out item))
                        {
                          SendEvent(item)
                        } else 
                        {
                          _ = reset_event.Reset();
                          _ = reset_event.WaitOne()
                        }
                      }
                    }
                    
                    public PostEvent(evt : Event) : void
                    {
                      queue.Enqueue(evt);
                      _ = reset_event.Set();
                    }
                    
                    //public IsEventDeclined : bool {get;private set;}
                    public Initiate() : void
                    {
                        queue_thread = Thread(QueueThread);
                        _ = reset_event.Reset();
                        Switch($initial);
                        queue_thread.Start();
                    }

                    public override ToString() : string
                    {
                        cur_state.ToString()
                    }

                    public static ContainsStates : array[string] = array[..$states];
                    public static ProcessEvents  : array[string] = array[..$events];
                    
                    mutable queue_thread : Thread;
                    queue : ConcurrentQueue[Event] = ConcurrentQueue();
                    reset_event : ManualResetEvent = ManualResetEvent(false);
                  }

                  ]>;

      Define(state_names |> <[decl: $(_[2] : usesite) : State.$(_ : usesite);]>); // create state variables for fast using 
      Define(dummy.td.GetMembers());
    }

    /// <summary>
    /// Define initial class structure
    /// </summary>
    DefineInit() : void
    {
      DefineMain();
      DefineStates(); // define states
      when (analyser.Fsm.flags.TransitionCompleted)
      {
        DefineEvent("TransitionCompleted");
      }
      DefineIsInState();
      DefineTerminate();
    }

    DefineTerminate() : void
    {
      def stop_activity(st, act)
      {
        def name = act.GetName(st.Data);
        <[
          $($"$(name)Stop" : usesite)();
        ]>
      }

      def stop_body = analyser.DoActivities |> stop_activity(_);
      def terminate = <[ decl: public Terminate() : void
                         {
                           {..$stop_body}
                           cur_state = null;
                           _ = reset_event.Set();
                           when (Thread.CurrentThread != queue_thread : object) queue_thread.Join();
                         }
                      ]>;
      Define(terminate);
    }

    DefineIsInState() : void
    {
      def create_comp_expr(st_id)
      {
        //def comp = TopState.Id.Result;
        def comp = ListUtils.Init(StatesId.Length, i => <[ cur_id.$($"id$i" : usesite) & id.$($"id$i" : usesite) == id.$($"id$i" : usesite) ]>);
        def expr = MergeExpr(comp, (x,y) => <[ $x && $y ]>);
        <[
          def id = $st_id;
          def cur_id = cur_state.get_id();
          $expr
        ]>
      }

      //def id = TopState.Id;

      def get_body(st_id)
      {
        if (StatesId.IsSimple)
        {
          match (st_id)
          {
            | <[ $(_ : usesite) ]> => <[ cur_state.get_id() & id == id ]>
            | _ => <[
                def id = $st_id;
                              cur_state.get_id() & id  == id 
                            ]>
          }
        } else
        {
          def ids = StatesId;
          def names = ListUtils.Init(ids.Length, i => $"id$i");
          def mem = if (ids.IsStruct)
                     {
                       def ids = names |> <[decl: public $(_ : usesite) : uint; ]>;
                       def vars = names.Map(x => <[parameter: $(x : usesite) : uint]>);
                       def assign = names |> <[ this.$(_ : usesite) = $(_ : usesite)]>;
                       def ctor = <[decl: public this(..$vars)
                                                      {
                                                        ..$assign
                                                      }]>;
                       def ids = ctor :: ids;
                        <[decl: struct LongId
                          {
                          ..$ids
                          } ]>
                     } else
                     {
                       def ids = names |> <[decl: public mutable $(_ : usesite) : uint; ]>;
                        <[decl: class LongId
                          {
                          ..$ids
                          }
                        ]>
                     }
          DefineNested(mem).Compile();
          create_comp_expr(st_id);
        }
      }
      def is_in_state = <[decl:
                public IsInState(st : State) : bool
                {
                  IsInState(st.get_id())
                }
            ]>;
      def id_type = StateIdType;
      def is_in_state2 = <[decl:
                public IsInState(id : $id_type) : bool
                {
                  $(get_body(<[ id ]>))
                }
            ]>;
      def st_is_in_state = <[decl: static IsInState(cur_state : State, st : State) : bool
                            {
                              $(get_body(<[st.get_id()]>))
                            }
                            ]>;
      Define([is_in_state, is_in_state2, st_is_in_state]);
    }


    Init() : void
    {

      InitActionNames();

      //tran_events = analyser.Events.;
      events = analyser.Events.NToList();
      events_call = Map(events |> (_, <[ cur_state.$(_ : usesite)(this) ]>));
      history_paths = analyser.HistoryPaths.MapToList(x => (x.Value.Name, GenAction.CreateActions(x.Value.Path, action_for)));
      //history_transitions = analyser.history_transitions.Map((x, y) => (x, GenAction.CreateActions(y.PathNode, action_for)));
      history_def_transitions = analyser.history_default_transitions.Map(x => (x.Value.Name, GenAction.CreateActions(x.Value.PathNode, action_for)));
    }

    InitActionNames() : void
    {
      def action_for_name(macros)
      {
        |  <[ $_($name) ]>  => name.ToString()
        | _                 => macros.ToString()
      }

      def class_decls  =  ty.GetParsedMembers(false);
      method_decls = class_decls.Filter(_ is ClassMember.Function) |> Name;
      def macros = class_decls |> (Name, _.ParsedAttributsAndModifiers.ParsedCustomAttributes);
      def macros = macros.Filter((_, x)  => !x.IsEmpty());
      macro_actions  =  Map(macros);
      decls = Map(class_decls |> (Name, _));

      def declared = decls.Keys();
      def action_for_decls =  macros.FindSingleList(_ is <[ ActionFor($_) ]>);
      def event_for_decls =  macros.FindSingleList(_ is <[ EventFor($_) ]>);
      def guard_for_decls = macros.FindSingleList(_ is <[ GuardFor($_) ]>);
      constructor =  macros.FindSingle(_ is <[Constructor]>).Map(x => x[0]);

      def decl = declared |> (_, _);
      def action = action_for_decls |> (action_for_name(_[2]), _);
      def evt = event_for_decls |> (action_for_name(_[2]), _);
      def methods = method_decls |> (_, _);
      action_for = Map(action + methods);
      event_for = Map(evt + methods);
      def guard = guard_for_decls |> (action_for_name(_[2]), _);
      def guard = guard + decl;
      def guard = guard.RemoveDuplicatesSort(((k1, _), (k2, _))  => k1.CompareTo(k2));
      guard_for = Map(guard);

      def acts = analyser.action_node_all;
      def acts = acts.Filter(x => {
                               | PathNode.ClearHistory
                               | PathNode.RunDoActivity
                               | PathNode.StopDoActivity => false
                               | _                       => true
                             });
      def acts = acts.SelectMany(GenAction.GetActions(_, action_for));
      def acts = acts.RemoveDuplicatesSort(_.action);
      actions = acts;
    }

  }

}
