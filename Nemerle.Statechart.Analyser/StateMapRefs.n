using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  /// <summary>
  /// Description of StateMapRefs.
  /// </summary>
  partial class StateMap
  {

    UpdateHistoryReference(hist : PseudoStateNode.History, st : FsmState) : PseudoStateNode.History
    {
      def default = hist.default.Map(UpdateTargetReference(_, st));
      hist.ChangeDefault(default)
    }

    UpdateStateReference(st : FsmState) : FsmState
    {
      | FsmState.SubMachine(type = type) as st =>
          def fsm = States.FilterType();
          match (fsm.Find(x => x.type == type))
          {
            | Some(FsmState.InlinedSubMachine as f) => UpdateStateReference(st.Instance(f))
            | _                                     => st
          }
      | _ => 

      def st = st.TransitionMap(UpdateTargetReference(_, st));
      st.HistoryMap(UpdateHistoryReference(_, st))
    }

    UpdateTargetReference(tr : StateTransition, st : FsmState) : StateTransition
    {
      def source = if (tr.Source != null) st else null;
	  
      def recursive_resolve(prev)
      {
        def to = prev.ReduceSingle(true, (x, _) => ResolveTargets(st, st, x, tr));
        if (to : object != prev) recursive_resolve(to) else to
      }
      
      
      tr.ChangeSourceNumTo(source, recursive_resolve(tr.To))
    }

    public NewReferences : bool {get;set;}
    
    GetNamed(tar : TransitionTarget.Named, tr : StateTransition) : TransitionTarget
    {
      match (tar)
      {
        | Named(name = n) =>

            def states = Analyser.PseudoStates.Filter(x => x.NativeName.HasValue);
            match (states.Find(x => x.NativeName.Value == n))
            {
              | Some(t) => TransitionTarget.PseudoState(Analyser, TransitionTarget.CreatePseudoRef(t))
              | _       => Analyser.AddError(FsmError.UnknownNode(tr));
                           tar
            }
      }
    }

    //public UpdateRefsToParent(st : FsmState) : void
    //{
    //  def num = st.Num;
    //  st.AllChilds.Iter(x => super_map[x] = num)
    //}
    
    GetNode[T](name : string, err : int -> void) : option[T] where T : PseudoStateNode
    {
      def pseudo = Analyser.PseudoStates.FilterType();
      def pseudo = if (name != null) pseudo.Where(x => if (x.NativeName is Some(n)) name == n else false) else pseudo;
      match (pseudo.NToList())
      {
        | []     => err(1);Some(null)
        | [node] => Some(node)
        | _      => err(2);None()
      }
    }
    
    CheckPseudo(st : FsmState, res : FsmState, np : ReferenceNode[PseudoStateNode], targ : TransitionTarget, tran : StateTransition) : ReferenceNode[PseudoStateNode]
    {
      if (np.IsFixed) null else
      match (np.Value)
      {
        | Fork as j => 
        //| EntryPoint
        //| ExitPoint =>
            //ParentFor(p.Num, st);
            //st.Super
            ParentFor(j.Num, st.Super);
            ChangeMan.Add(st.Super.Num, x => x.AddPseudo(j));
            np.Fix(j.Num)
            
        | History as t       => def hist = res.GetHistory(t);
                                np.Fix(hist.Num)
                                
        | Junction(IsUnnamed = true) as j => ParentFor(j.Num, st.Super);
                                             ChangeMan.Add(st.Super.Num, x => x.AddPseudo(j));
                                             np.Fix(j.Num)
                                             
        | Junction(name = n) => match (GetNode(n, _ => Analyser.AddError(FsmError.UnknownTarget(tran, targ))))
                                {
                                  | Some(null)          => np
                                  | Some(Junction as j) => np.Fix(j.Num)
                                  | _                   => np
                                }
        | Join(name = n)     => match (GetNode(n, _ => Analyser.AddError(FsmError.UnknownTarget(tran, targ))))
                                {
                                  | Some(null)      => np
                                  | Some(Join as j) => np.Fix(j.Num)
                                  | _               => np
                                } 
        | Terminate as t     => match (GetNode(null, n => when (n == 2) Analyser.AddError(FsmError.UnknownTerminateTransition(tran))))
                                {
                                  | Some(null)           => ParentFor(t.Num, st.Super);
                                                            ChangeMan.Add(st.Super.Num, x => x.AddPseudo(t));
                                                            np.Fix(t.Num)
                                  | Some(Terminate as t) => np.Fix(t.Num)//TransitionTarget.PseudoState(this, node)
                                  | _                    => np
                                }
        | _                  => null
      }
    }        

    
    // TODO: решить проблему с двойным обходом узлов до разрешения и после
    ResolveTargets(st : FsmState, res : FsmState, tr : TransitionTarget, tran : StateTransition) : TransitionTarget
    {
      match (tr)
      {
        | State(state = cst, value = r) as t when (!r.IsFixed) => def cst = cst.Updated;
                                                                  t.ChangeValue(r.Fix(cst.Num))

        | Named as t => def t = GetNamed(t, tran);
                        ResolveTargets(st, st, t, tran)
        | Qualified(parent = res, target = t) => 
            def res = Get(res);
            match (res, t)
            {
              | (FsmState.SubMachine as m, TransitionTarget.State(state = FsmState(Name = n))) =>
                  def m = m.Scan(); // scan external sub machine for states
                  when (!m.ref_sub_states.Contains(n)) Analyser.AddError(FsmError.SubMachineStateNotFound(m, n));
                  TransitionTarget.Nowhere(Analyser)//TransitionTarget.SubMachine(Analyser, m, n)

              | (null, _) => Analyser.AddError(FsmError.UnknownTarget(tran, tr));
                             t
              | (res, t)  => ResolveTargets(st, res, t, tran)
            }
        | PseudoState(p) as t => def np = CheckPseudo(st, res, p, t, tran);
                                 if (np == null) null else t.ChangeNode(np) 

        | _                   => null
      }
    }


  }
}
