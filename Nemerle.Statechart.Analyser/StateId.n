using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler.Parsetree;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Collections;

namespace Nemerle.Statechart
{

  using FsmState;
  using BitUtils;

  [Record]
  public class IdType
  {
    public val : BitArray;

    public IsSimple : bool
    {
      get
      {
        Length <= 2
      }
    }

    public IsStruct : bool
    {
      get
      {
        def len = Length;
        len > 2 && len <= 4
      }
    }

    public Count : int
    {
      get
      {
        val.Count
      }
    }

    public Length : int 
    {
      get
      {
        MathUtils.UpperDiv(Count, 32)
      }
    }

    public Or(id : IdType) : IdType
    {
      def id = val | id.val;
      IdType(id)
    }

    public static @|(id : IdType, id2 : IdType) : IdType
    {
      id.Or(id2)
    }
    
    public UInt : uint
    {
      get
      {
        Result[0] :> uint
      }
    }

    public ULong : ulong
    {
      get
      {
        def r1 = Result[0] :> uint;
        def r2 = Result[1] :> uint;
        BitUtils.MakeLong(r1, r2)
      }
    }

    public Result : array[int]
    {
      get
      {
        def res = array(Length);
        val.CopyTo(res, 0);
        res
      }
    }

  }

  /// <summary>
  /// State identifier
  /// </summary>
  [Record]
  public class StateId
  {
    analyser : FsmAnalyser;

    count : int
    {
      get
      {
        analyser.statemap.Count
      }
    }

    public CreateMasks() : FsmState 
    {
      def st = analyser.TopState;

      def create_num_id_super(s, t)
      {
        def id = s.Id | t;
        s.ChangeId(id)
      }

      def create_num_id(s, t)
      {
        def id = BitArray(count);
        def num = t.Num;
        when (num != 0) id.Set(num, true);
        def id = IdType(id);
        def sub = s |> create_num_id_super(_, id);
        t.ChangeIdSubStates(id, sub)
      }

      def mark_inlined_inst(sub, t)
      {
        match (t)
        {
          | FsmState where (cloneFor = FsmState where (Id = clone_id, PartOfStateMachine = Some(FsmState where (super_state = FsmState where (Id = inlined_super_id))))) =>

              def id = t.Id;
              def id = (id.val | clone_id.val);
              def id = id & ~inlined_super_id.val;
              def id = IdType(id);
              t.ChangeIdSubStates(id, sub)
          | _ => t     
        }
      }

      // first pass
      def st = st.FoldThis(create_num_id);
      // second pass                    
      st.FoldThis(mark_inlined_inst);
    }

  }
}
