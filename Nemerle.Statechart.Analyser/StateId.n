using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler.Parsetree;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Collections;


namespace Nemerle.Statechart
{

  using FsmState;
  using BitUtils;

  [Record]
  public class IdType
  {
    public val : BitArray;

    public Count : int
    {
      get
      {
        val.Count
      }
    }

    public Length : int 
    {
      get
      {
        MathUtils.UpperDiv(Count, 32)
      }
    }

    public Or(id : IdType) : IdType
    {
      def id = val | id.val;
      IdType(id)
    }

    public static @|(id : IdType, id2 : IdType) : IdType
    {
      id.Or(id2)
    }

    public UInt : uint
    {
      get
      {
        Result[0] :> uint
      }
    }

    public ULong : ulong
    {
      get
      {
        unchecked 
        {
          def r1 = Result[0] :> uint;
          def r2 = Result[1] :> uint;
          BitUtils.MakeLong(r1, r2)
        }
      }
    }

    public Result : array[int]
    {
      get
      {
        def res = array(Length);
        val.CopyTo(res, 0);
        res
      }
    }

  }

  /// <summary>
  /// State identifier
  /// </summary>
  [Record]
  public class StateId
  {
    analyser : FsmAnalyser;

    public IsSimple : bool
    {
      get
      {
        Length <= 2
      }
    }

    public IsStruct : bool
    {
      get
      {
        def len = Length;
        len > 2 && len <= 4
      }
    }

    public Length : int 
    {
      get
      {
        MathUtils.UpperDiv(Count, 32)
      }
    }

    Count : int
    {
      get
      {
        analyser.statemap.States.Count()
      }
    }

    public CreateMasks() : void
    {
      def st = analyser.States;
      def idx = st.MapIndex((x, i) => (x, i));
      def map = Hashtable(idx);

      def create_id(t)
      {
        def id = BitArray(Count);
        def num = map[t];
        id[num] = true;
        IdType(id)
      }
      
      // first pass
      foreach (s in st)
      {
        def id = create_id(s);
        def id = if (s.Super != null) id | s.Super.Id else id;
        s.Id = id
      }
    }

  }
}
