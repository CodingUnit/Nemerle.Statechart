using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler.Parsetree;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Collections;

namespace Nemerle.Statechart
{

  using FsmState;
  using BitUtils;

  [Record]
  public class IdType
  {
    public val : BitArray;

    public Count : int
    {
      get
      {
        val.Count
      }
    }

    public Length : int 
    {
      get
      {
        MathUtils.UpperDiv(Count, 32)
      }
    }

    public Or(id : IdType) : IdType
    {
      def id = val | id.val;
      IdType(id)
    }

    public static @|(id : IdType, id2 : IdType) : IdType
    {
      id.Or(id2)
    }

    public UInt : uint
    {
      get
      {
        Result[0] :> uint
      }
    }

    public ULong : ulong
    {
      get
      {
        unchecked 
        {
          def r1 = Result[0] :> uint;
          def r2 = Result[1] :> uint;
          BitUtils.MakeLong(r1, r2)
        }
      }
    }

    public Result : array[int]
    {
      get
      {
        def res = array(Length);
        val.CopyTo(res, 0);
        res
      }
    }

  }

  /// <summary>
  /// State identifier
  /// </summary>
  [Record]
  public class StateId
  {
    analyser : FsmAnalyser;

    public IsSimple : bool
    {
      get
      {
        Length <= 2
      }
    }

    public IsStruct : bool
    {
      get
      {
        def len = Length;
        len > 2 && len <= 4
      }
    }

    public Length : int 
    {
      get
      {
        MathUtils.UpperDiv(Count, 32)
      }
    }

    Count : int
    {
      get
      {
        analyser.statemap.States.Count()
      }
    }

    public CreateMasks() : void
    {
      def st = analyser.TopState;

      def create_id(t)
      {
        def id = BitArray(Count);
        def num = t.Num;
        id.Set(num, true);
        IdType(id)
      }

      def create_num_id_super(s, t : IdType)
      {
        def id = create_id(s);
        def id = if (t != null) id | t else id;
        s.ChangeId(id)
      }

      def create_num_id(t)
      {
        def id  = t.Id;
        def sub = t.sub_states.Map(create_num_id_super(_, id));
        t.ChangeSubStates(sub)
      }

      def mark_inlined_inst(t)
      {
        | FsmState(CloneFor = null) => null
        | FsmState(CloneFor = FsmState(Id = clone_id, PartOfStateMachine = Some(FsmState(Super = FsmState(Id = inlined_super_id)))), Id = id) =>

              def clone_id = clone_id.val & ~inlined_super_id.val; // get clone id but without it super state environment
              def id = (id.val | clone_id); // merge current id and from it is cloned
              def id = IdType(id);
              t.ChangeId(id)

        | _ => null
      }

      // first pass
      def st = st.LevelOrderMap(create_num_id).NToList();
      analyser.UpdateStateMap(st);
      // second pass
      def st = analyser.TopState.LevelOrderMap(mark_inlined_inst).NToList();
      analyser.UpdateStateMap(st)
    }

  }
}
