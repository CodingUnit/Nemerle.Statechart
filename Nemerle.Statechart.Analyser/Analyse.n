// analyser.n
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Statechart;
using NGenerics.DataStructures.Trees;

namespace Nemerle.Statechart
{

  using StateTransition;
  using ListUtils;

  public partial class FsmAnalyser
  {
    
    InitStatechart() : void
    {
      def state = Fsm.state.ChangeName(FsmState.TopStateName);

      StateMap.Create(this, state)

    }

    InitStateMap() : void
    {
      InitStatechart();

      CreateHistoryStates();
      when (Error) throw FsmErrorException();
      RenameDuplicates()
    }
    
    CheckAddFinalState() : void
    {
      def check_target(tran)
      {
       def to = tran.Value.To;
       def st = tran.Parent;
       def to = match (to, tran.Value)
                  {
                // TODO: check later for need of termination of fsm if we reach final state
                    | (TransitionTarget.Final as t, StateTransition.Initial) with super = st
                    | (Final as t, _) with super = st.Parent => 
                        match (super.ChildNodes.Find(x => x.Data is FsmState.Final))
                        {
                          | None => def fin = FsmState.Final(this);
                            def fin = GeneralTree(fin);
                            //super.Add(fin);
                            statemap.Add(super, fin);
                            t.ChangeState(fin);

                          | Some(fin)  => t.ChangeState(fin)
                        }
                    | _ => to
       

                  }
                  
        tran.Value = tran.Value.ChangeTo(to)
      } 
     
      TransitionEnumerate.Iter(check_target);

     
    }

    States : IEnumerable[GeneralTree[FsmState]]
    {
      get
      {
        statemap.States
      }
    }

    TransitionEnumerate : IEnumerable[NodeValue[FsmState, StateTransition]]
    {
      get
      {
        def en = statemap.StateTree.GetEnumerable(TreeTraversalType.BreadthFirst);
        en.SelectMany(x => x.AllTransitions())
      }
    }
    
    RenameDuplicates() : void
    {
      def rename_group(st)
      {
        st.IterI((i, x) => x.Update(x => x.ChangeCopyNum(i)))
      }

      def dups = States.NToList().Duplicates((x, y) => FsmState.NameComparer(x, y));
      def dups = dups.GroupBy(x => x.Name());

      dups.Iter(rename_group)
    }

    CreateHistoryStates() : void
    {
    // check history duplicates in state
      def check_dup_history(st)
      {
        match (st.History())
        {
        // duplicate variants
          | TwoOption.Two(n, n2) =>
              match (n.Value, n2.Value)
              {
                | (PseudoStateNode.History(type = HistoryType.Deep), PseudoStateNode.History(type = HistoryType.Deep))
                | (PseudoStateNode.History(type = HistoryType.Shallow), PseudoStateNode.History(type = HistoryType.Shallow)) => AddError(FsmError.DuplicateHistory(st.Data, st.History().Value.First()))
                | _                                                                                                           => ()
              }
          | _ => ()
        }
      }

      def check_exit_actions(st)
      {
        | FsmState(exit = ex) when ex.IsEmpty() => st.ChangeExit(StateAction.Exit([], ActionFlags.None) :: [])
        | _                                     => st
      }

      def update_hist(st)
      {
        when (st.History().HasValue)
        {
          check_dup_history(st);
          st.Update(check_exit_actions);
        }
      }

      States.Iter(update_hist)
    }

    CreateActions() : void
    {
      def check_do(st, act)
      {
        def check(act)
        {
            | StateAction.Do(actions = [_], Concurrently = true, flags = f) => 
                def err = FsmWarning.NoConcurrentActivity(act);
                AddWarning(err);
                act.ChangeFlags(f & ~ActivityFlag.Concurrently);
            | _ => act
        }

        def act = act.Map(check);
        st.Update(x => x.ChangeDoActivity(act))
      }

      States.Iter(x => check_do(x, x.Data.do_activity));
    }

    CreateConcurrentRegion() : void
    {
      def warn = not_used_regions.Map(FsmWarning.RegionNotUsed);
      AddWarnings(warn)
    }

    CreateHierarchy() : void
    {

      def set_hierarchy_pos(st)
      {
        if (st.IsTopState()) st.Update(x => x.ChangeCalcHierarchyPos(0)); 
        else st.Data = st.Data.ChangeCalcHierarchyPos(st.Parent.Data.calc_hierarchy_pos + 1)
      }

      States.Iter(set_hierarchy_pos)
    }


    CreateSubMachines() : void
    {
      /*def inlined = States.Where(x => x.Data is FsmState.InlinedSubMachineInstance);
      inlined.Iter(x => x.InitInline());*/
            // TODO: надо делать обновление ссылок только один раз
      }


    public Analyse() : void
    {
      try
      {
        CheckParseErrors();
        InitStateMap();
        CheckTransitions();
        CheckAddFinalState();
        CreateSubMachines();
        CreateHierarchy(); // create hierarchy
        CheckInitialTransitions();
        CreateTransitions();
        BreakIfError();
        CreateTransitionPaths();
        CreateHistoryPaths(); // create history paths
        CreateConcurrentRegion();
        UpdateInitial();
        CreateActions();
        FinalizeAnalyse();
      }
      catch 
      {
        | x is FsmErrorException => 
          def err = x.error;
          when (err != null) AddError(err);
      }
    }

  }
}