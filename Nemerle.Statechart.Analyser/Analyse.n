using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Statechart;

namespace Nemerle.Statechart
{

  using StateTransition;
  using ListUtils;

  public partial class FsmAnalyser
  {


    UpdateTransitions(tran : IEnumerable[StateTransition]) : void
    {
      def group = tran.GroupBy(x => x.Source.Num);
      def st = group.Map(x => statemap[x.Key].UpdateTransitions(x));
      statemap.Update(st)
    }

    InitStatechart() : void
    {
      def state = Fsm.state.ChangeName(FsmState.TopStateName);
      def top = state.Fold(null, 
                           (s, sb) => FsmState.CreateState(s, sb, this),
                           (s, sr) => FsmState.ConcurrentRegion(s, sr.Map(_ :> FsmState.ConcurrentSubRegion), this),
                           (s, _)  => FsmState.Final(s, this),
                           null);
                           
      UpdateStateMap(top);
      
      
    }

    InitStateMap() : void
    {
      InitStatechart();
      
      CreateHistoryStates();
      when (Error) throw FsmErrorException();
      RenameDuplicates()
    }

    
    public UpdateStateMap(st : IEnumerable[FsmState]) : void
    {
      statemap.Update(st)
    }
    
    public UpdateStateMap(top : FsmState = null) : void
    {
    //InitStates();
      statemap.Update(top ?? TopState);
      //when (upd_ref) 
      //{
      //  def top = TopState.ReduceSingle((x, _) => UpdateStateReference(x));
      //  statemap.Update(top)
      //}
    }

    CheckAddFinalState() : void
    {
        def check_target(st, t, tran, a)
        {
          match (t, tran)
          {
            //| (TransitionTarget.Final, _) when (is_top(st)) => 
            //    def term = PseudoStateNode.Terminate(0, statemap.GetNextGlobalNum(), this);
            //    (TransitionTarget.PseudoState(this, term), null :: a)
                // TODO: check later for need of termination of fsm if we reach final state
            | (TransitionTarget.Final as t, StateTransition.Initial) with super = st
            | (Final as t, _) with super = st.Super => 
              match (a.Find((x, _) => x.Num == super.Num))
              {
                | Some((_, fin)) => def t = t.ChangeState(t.state.Fix(fin.Num));
                                    (t, a)
                | _              => def fin = FsmState.Final(this);
                                    def fin = fin.SetNewId();
                                    def t = t.ChangeState(t.state.Fix(fin.Num));
                                    (t, (super, fin) :: a)
              }
              
              
          }
        }

        def check_final(f, st, sub)
        {
          if (st.HaveFinal) st else
          match (f.Find((x, _) => x.Num == st.Num))
          {
            | Some((_, fin)) => st.ChangeSubStates(sub.Append(fin).NToList())
            | _              => null
          }
        }

        def check_tran(st, (x, (tr, a)))
        {
          def (to, fin) = x.To.Reduce(a, final = check_target(st, _, x, _));
          def tran = x.ChangeTo(to);
          (tran :: tr, fin)
        }

        def check_state(s, _, a)
        {
          def (tran, fin) = s.transitions.FoldBack(([], a), check_tran(s, _));
          def (init, fin) = s.initial.FoldBack(([], fin), check_tran(s, _));
          def st          = s.ChangeTransitionsInitial(tran, init);
          (st, fin)
        }

        def (top, final) = TopState.ReduceSingle([], check_state);
        // check to add final state
        UpdateStateMap(top.ReduceSingle(true, check_final(final, _, _)))
    }
    
    RenameDuplicates() : void
    {
      def rename_group(st)
      {
        st.MapIndex(_.ChangeCopyNum(_))
      }

      def dups = States.NToList().Duplicates(FsmState.NameComparer);
      def dups = dups.GroupBy(_.Name);

      def dups = dups.SelectMany(rename_group);
      statemap.Update(dups)
    }

    CreateHistoryStates() : void
    {
    // check history duplicates in state
      def check_dup_history(st)
      {
        match (st.history)
        {
        // duplicate variants
          | TwoOption.Two(PseudoStateNode.History(type = HistoryType.Deep), PseudoStateNode.History(type = HistoryType.Deep))
          | TwoOption.Two(PseudoStateNode.History(type = HistoryType.Shallow), PseudoStateNode.History(type = HistoryType.Shallow)) => AddError(FsmError.DuplicateHistory(st, st.history.Value.First()))
          | _                                                                                                                       => ()
        }
      }

      def check_exit_actions(st)
      {
        | FsmState(exit = ex) when ex.IsEmpty() => st.ChangeExit(StateAction.Exit([], ActionFlags.None) :: [])
        | _                                     => st
      }

      def update_hist(s, _)
      {
        if (s.history.HasValue)
        {
          check_dup_history(s);
          check_exit_actions(s);
        } else null
      }

      def top = TopState.ReduceSingle(update_hist);
      statemap.Update(top)
    }

    CreateActions() : void
    {
      def check_do(st, act)
      {
        def check(act)
        {
            | StateAction.Do(actions = [_], Concurrently = true, flags = f) => 
                def err = FsmWarning.NoConcurrentActivity(act);
                AddWarning(err);
                act.ChangeFlags(f & ~ActivityFlag.Concurrently);
            | _ => act
        }

        def act = act.Map(check);
        st.ChangeDoActivity(act)
      }

      def top = TopState.ReduceSingle((s, _) => check_do(s, s.do_activity));
      statemap.Update(top);
    }

    CreateConcurrentRegion() : void
    {
      def warn = not_used_regions |> FsmWarning.RegionNotUsed(_);
      AddWarnings(warn)
    }

    CreateHierarchy() : void
    {

      def set_hierarchy_pos(st)
      {
        def sub = st.sub_states.Map(x => x.ChangeCalcHierarchyPos(st.calc_hierarchy_pos + 1));//, st :: st.super_states
        st.ChangeSubStates(sub)
      }

      def top    = TopState.ChangeCalcHierarchyPos(0);
      def states = top.LevelOrderMap(set_hierarchy_pos);
      statemap.Update(states)
    }


    CreateSubMachines() : void
    {
      def (top, base_sm_types) = TopState.Reduce(true, [], inlined_sub_machine_instance = (x, _, acc) => x.InitInline(acc));
      // TODO: надо делать обновление ссылок только один раз
      UpdateStateMap(top); // update tree with instanced state machines
      UpdateStateMap(base_sm_types.SelectMany(_.AllStates)); // update base sm types and its contents
    }
    
    
    public Analyse() : void
    {
      try
      {
        CheckParseErrors();
        InitStateMap();
        CheckTransitions();
        CheckAddFinalState();
        CreateSubMachines();
        CreateHierarchy(); // create hierarchy
        CheckInitialTransitions();
        CreateTransitions();
        BreakIfError();
        CreateTransitionPaths();
        CreateHistoryPaths(); // create history paths
        CreateConcurrentRegion();
        UpdateInitial();
        CreateActions();
        FinalizeAnalyse();
      }
      catch 
      {
        | x is FsmErrorException => 
           def err = x.error;
           when (err != null) AddError(err);
      }
    }
    
  }
}
