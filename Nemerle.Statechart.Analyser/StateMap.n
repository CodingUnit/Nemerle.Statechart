using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Collections;

using NC = Nemerle.Collections;

namespace Nemerle.Statechart
{
  using FsmState;

  [Record]
  public class StateMap
  {
    mutable states : array[FsmState] = array[];

    public Analyser : FsmAnalyser;

    public namemap : Hashtable[string, int] = Nemerle.Collections.Hashtable();
    public tranmap : Hashtable[string, int] = Nemerle.Collections.Hashtable();

    mutable super_map : array[int] = array[];
    
    public GetNewStateId(state : FsmState) : int
    {
      match (namemap.Get(state.NativeName))
      {
        | Some(num) => num
        | _         => def num = GetNextStateNum();
                       namemap[state.NativeName] = num;
                       num
      }
    }

    public GetNewTransitionId(tran : StateTransition) : int
    {
      match (tranmap.Get(tran.Name))
      {
        | Some(num) => num
        | _         => def num = GetNextTransitionNum();
                       tranmap[tran.Name] = num;
                       num
      }
    }

    [Accessor]
    mutable state_num      : int;

    [Accessor]
    mutable transition_num : int;

    public GetNextStateNum() : int
    {
      def num = state_num;
      state_num++;
      num
    }

    public GetNextTransitionNum() : int
    {
      def num = transition_num;
      transition_num++;
      num
    }

    public States : array[FsmState]
    {
      get
      {
        states
      }
    }

    public this(an : FsmAnalyser) { Analyser = an; TopNum = -1 }

    public Contains(state : FsmState) : bool
    {
      states.Length - 1 >= state.Num
    }

    public static GetFullName(st : list[FsmState]) : string
    {
      string.Join(".", st |> NativeName)
    }

    public Get(state : FsmState) : FsmState
    {
      if (state == null) null else
      {
        def num = state.Num;
        def st = if (num == -1) Get(state.NativeName) else Get(num);
        match (st)
        {
          | null => throw InvalidOperationException($"state is not correctly mapped $state")
          | st   => st
        }
      }
    }

    public Get(state : list[string]) : FsmState
    {
      def merge_name(n)
      {
        string.Join(".", n)
      }

      def get_loop(st, lst, path)
      {
        | (st, head :: tail, p) => 
              def path = head :: p;
              match (get_all_state(st, head), tail)
              {
                | ([], _)    => Analyser.AddError(FsmError.StateNotFound(State(merge_name(path.Rev()), Analyser))); null
                | ([st], []) => st
                | ([st], _)  => get_loop(st.sub_states, tail, path)
                | (lst, _)   => Analyser.AddError(FsmError.SeveralState(lst |> State(merge_name((_.NativeName :: p).Rev()), Analyser)));
                                null
              }
        | _                => null
      }

      get_loop(states.NToList(), state, [])
    }

    get_all_state(map : IEnumerable[FsmState], state : string) : list[FsmState]
    {
      def name = state ?? FsmState.TopStateName;
      def (name, sub_name) = if (name != FsmState.TopStateName) 
                              {
                                def sub =  name.GetSub('[', ']');
                                if (sub != null) (name.GetSub('['), sub) else (name, null)
                              } else (name, null);
      def name = if (sub_name != null) $"$(name)SubRegion$sub_name" else name;
      map.Filter(x => x.NativeName == name)
    }


    public Get(state : string) : FsmState
    {
      def st = state.SplitToList('.');
      Get(st)
    }

    public Get(num : int) : FsmState
    {
      if (num != -1) states[num] else null
    }

    public Item[index : int] : FsmState
    {
      get
      {
        Get(index)
      }
    }


    public Update(st : IEnumerable[FsmState], top : FsmState = null) : void
    {
      
      def check_name(n, _)
      {
        match (st.Find(_.StateEquals(n)))
        {
          | Some(n) => n
              // if no new state use old
          | _       => null
        }
      }

      def top = Top ?? top;
      def top = top.ReduceSingle(check_name); // replace old states with new ones
      Update(top)
    }

    //public DefineState(st : FsmState, super : FsmState) : void
    //{
    //  super_map[st.Num] = super.Num
    //}
    
    public Update(top : FsmState) : void
    {
      
      def check_super(s)
      {
        Enumerable.Repeat(s.Num, s.sub_states.Length)
      }
      
      def check_new_super(s)
      {
        def num = s.Num;
        s.sub_states.Iter(x => super_map[x.Num] = num)
      }
      
      // if top is not top state, but some child
      if (Top != null && Top.Num != -1 && top.Num != Top.Num)
      {
        Update(TraverseUtils.SingleElement(top))
      } else
        when (top != Top)
        {
          if (TopNum == -1) // if not set number
          {
            def st = top.LevelOrderMap(x => x.ChangeNum(GetNextStateNum())).ToArray();
            TopNum = 0;
            st.Iter(x => namemap[x.NativeName] = x.Num);
            Update(st, top)
          } else
          {
            def st = top.AllStates;
            states = st.OrderBy(_.Num).ToArray();
            // update super state map
            // if new map
            if (super_map.Length != states.Length)
            {
              // create new map
              def supers = states.SelectMany(check_super);
              super_map = supers.AddBefore(-1).ToArray();
            } else
            {
              // else update
              states.Iter(check_new_super)
            }
          }
        }
    }

    public Super(st : FsmState) : FsmState
    {
      def num = st.Num;
      if (num != -1) Get(super_map[num]) else null
    }
    
    public Count : int
    {
      get
      {
        states.Length
      }
    }

    public TopNum : int {get; private set;}

    public Top : FsmState
    {
      get
      {
        if (Count == 0) null else states[0]
      }
    }
  }
}
