using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  using StateTransition;
  using ListUtils;
  //using DecisionNode;  
  using GuardNode;
  using Opt;
/// <summary>
/// Description of AnalyseTransitions.
/// </summary>
  public partial class FsmAnalyser
  {

    CheckInitialTransitions() : void
    {
      def check_auto_initial(st)
      {
        | FsmState(type = StateType.Final) 
        | FsmState(type = StateType.Region) => ()
        | FsmState(initial = None, sub_states = head :: _) when (!head.IsFinal) => st.DefineInitial(head);
        | _ => ()
      }

      def check_init_tran_correct(t)
      {
        match (t.pseudo_states.FilterType())
        {
          | []           => ()
          | t1 :: _ :: _ => AddError(FsmError.InitialPseudoStateAtMostOne(t1))
          | PseudoStateNode.Initial(tran = tran) as i :: [] => 
              match (tran.To)
              {
                | PseudoState(PseudoStateNode.History(state = st)) 
                | PseudoState(PseudoStateNode.EntryPoint(state = st))
                | TransitionTarget.State(st) when (t.AllSubStates.Contains(st)) => ()
                | _                                                             => AddError(FsmError.InitialTransitionMustGoToState(i))
              }
              match (tran.To)
              {
                | PseudoState(PseudoStateNode.History(default = None)) => AddError(FsmError.InitialTransitionToHistoryMustHaveDefaultTran(i))
                | _                                                    => ()
              }
              when (tran.guard.NotEmpty) AddError(FsmError.InitialTransitionCannotHaveGuards(i));
        }
        
      }
      
      
      // check state attributes
      foreach (st in States)
      {
        check_init_tran_correct(st); // check for correct initial transitions
        when (st.Attributes.Exists(_ is StateAttribute.Initial))
        {
          st.Super.DefineInitial(st)
        }
        when (FsmFlags.AutoInitial) check_auto_initial(st);
      }
      
      check_auto_initial(TopState);
      foreach (st when st.IsComposite in States)
      {
        when (!st.IncomingTransitions.IsEmpty())
        if (st.IsConcurrent)
        {
          def sreg = st.sub_states.Where(x => x.initial.IsNone);
          when (!sreg.IsEmpty())
          {
            AddError(FsmError.SubRegionsMustContainInitial(sreg.NToList()))
          }
        } else
        {
          when (st.initial.IsNone) AddError(FsmError.CompositeStateWithIncomingTransitionsMustHaveInitial(st))
        }
      }
      
    }
    
    CheckConflictTransitions(st : FsmState, trans : IEnumerable[StateTransition]) : void
    {
      def conflict = trans.OrderByDescending(x => x.From.hierarchy_pos).NToList();
      def by_hierarchy = conflict.GroupBy(x => x.From.hierarchy_pos);
      
      // check for duplicates
      foreach (tr in by_hierarchy)
      {
        def dups = tr.NToList().Duplicates(FsmState.Compare(StateTransition.GuardComparer)); // find duplicates with same guards and same triggers
        when (!dups.IsEmpty())
        {
          AddError(FsmError.DuplicateTransition(dups))
        }
      }
      
      def search_for_no_guard(g)
      {
        if (!g.IsMoreThanOne()) g else
          match (g.Find(x => x.guard.IsEmpty))
          {
            | Some(t) => def tr   = g.Except(t); // without guard already enabled and have more priority
                         def warn = tr.Map(FsmWarning.TransitionOverriden(_, t)); // signal this, not needed declaration, design error
                         AddWarnings(warn);
                         ListUtils.SingleElement(t)

            | _       => g
          }
      }


      def resolve_on_levels = by_hierarchy.SelectMany(search_for_no_guard).NToList();
      //def del = trans.Except(resolve_on_levels);
      def (left, _) = resolve_on_levels.SplitFind(x => x.guard.IsEmpty);
      def del = trans.Except(left);
      st.DeleteTransitions(del);// conflicted deleted from set of enabled without signal, normal situation
    }
    
    
    //FlattenTransitions(st : IEnumerable[FsmState] = null) : void
    //{
    //  def states = st ?? States;
    //  // create super state transitions in simple states
    //  foreach (st when st.Super != null in states)
    //  {
    //    st.DefineTransitions(st.Super.transitions)
    //  }
      
    //  foreach (st when st.IsComposite in states)
    //  {
    //    st.DeleteTransitions() // delete other transitions from composite states
    //  }
    //}
    
    CreateAndCheckConflictTransitions() : void
    {
      //FlattenTransitions();
      //def simple = States.Where(_.IsSimple);

      foreach (st in States)
      {
        foreach (group in st.ConflictTransitions)
        {
          //CheckConflictTransitions(st, group);
        }
      }
    }

    CheckDeferedEvents() : void
    {
      def check_override(tr, st, defer)
      {
        // transitions with trigger exists in defered list
        def tr = tr.Where(x => defer.Exists(y => x.trigger == y));
        // filter and partition transition with position in hierarchy lower or equal current state
        def (left_tr, removed) = tr.PartitionLazy(x => x.From.hierarchy_pos >= st.hierarchy_pos);
        // left_tr transitions that overrides defered events in current state
        def left_events = left_tr.Map(_.trigger); // left events that override other defered
        def left_defer = defer.Except(left_events).NToList(); // remove from defer that events
        //def tr = tr.Except(removed); // remove from transition list removed events
        (removed, left_defer)
      }
      
      foreach (st in States)
      {
        when (st.Super != null) 
        {
          def evts = st.Super.deferred_events; // super state deffered events
          def (rem_tran_evts, evts) = check_override(st.transitions, st.Super, evts); // transition events in current state
          st.DeleteTransitions(rem_tran_evts); // removed transitions overriden by defered events
          st.deferred_events += evts;
        }
      }
    }
    
#if 0    
    create_join_transitions() : void
    {

      def check_join(j, tr : IEnumerable[_])
      {
        match (j)
        {
          | PseudoStateNode.Join(target = t) as jn =>

              if (tr.ElementsEqual((x, y) => x.Trigger == y.Trigger))
              {
                if (tr.All(x => x.Source.PartOfSubRegion.HasValue) &&
                    tr.ElementsEqual((x, y) => x.Source.PartOfRegion.Value == y.Source.PartOfRegion.Value : object))
                {
                  def tran = StateTransition(j.state, t, tr.First().Trigger);
                  def reg = tr.First().Source.PartOfRegion.Value;
                  def tr = tr.OrderBy(x => x.Source.PartOfSubRegion.Value.Index);
                  //j.joined_tran = tr;
                  //j.region = reg;

                  //j.transition = tran;
                  /// !!! mutable
                  //tr.Iter(x => x.ToPseudo.SetValue(x => (x :> PseudoStateNode.Join).ChangeRegionJoinedTranTransition(reg, tr, tran)));
                  jn.region = reg;
                  jn.joined_tran = tr.NToList();
                  jn.transition = tran;
                } else 
                {
                  AddError(FsmError.JoinTransitionMustLeaveRegion(j, tr.NToList()));
                }
              } else 
              {
                AddError(FsmError.JoinTransitionMustHaveSameTrigger(j, tr.NToList()))
              }
        }
      }

      def get_join(t, lst)
      {
        match (t)
        {
          | StateTransition(To = TransitionTarget.PseudoState(PseudoStateNode.Join as j)) => (j, t) :: lst
          | _                                                                             => lst
        }
      }

      def join_tran = StateTransitions.FoldLeft([], get_join);
      def jtrans = join_tran.GroupBy((x, _) => x);
      def jtrans = jtrans.Map(x => (x.Key, x.Values()));
      jtrans.Iter(check_join)
    }

    create_fork_transitions() : void
    {
      def check_fork(f)
      {
        | PseudoStateNode.Fork(target = t) =>

              def st = t.Select(x => x.ToState);
              def reg = st.Select(x => x.PartOfSubRegion);
              if (!reg.IsEmpty() && reg.All(x => x.HasValue))
              {
                //def sreg = reg |> Last();
                def dups = reg.DuplicatesGroup(x => x.Value.Index);
                def regs = st.Select(x => x.PartOfRegion.Value);
                if (dups.Count() != 0 || !regs.ElementsEqual((x, y) => x == y : object)) 
                {

                  AddError(FsmError.ForkMustGoToSeparateRegion(f))
                } else
                {
                  //def reg = regs.First();
                  def sub_trans = t.Map(x => StateTransition.Transition(f.state, x, null));
                  f.fork_tran = sub_trans;
                  //def sub_trans = sub_trans.Map(x => x.ChangeLocal(true));
                  //tr.ToPseudo.SetValue(x => (x :> PseudoStateNode.Fork).ChangeForkTran(sub_trans))
                }
              } else
              {
                AddError(FsmError.ForkMustGoToRegion(f))
              }

      }

      def tran = StateTransitions.GetPseudo();
      tran.Iter(check_fork)
      //statemap.Update();
    }
#endif
    CheckLocalTrans() : void
    {
      foreach (tr when tr.kind == TransitionKind.Local in StateTransitions)
      {
        def src = match (tr.FromPseudo)
        {
          | Some(PseudoStateNode.EntryPoint as p) => p.state
          | _                                     => tr.From
        }
        def dest = tr.AtLeastStaticTo;
        if (dest.IsSubstateOf(src) || src == dest : object) () else
        {
          AddError(FsmError.LocalTransitionTargetMustBeWithin(tr))
        }
      }
    }
    
    CheckTransitions() : void
    {
      // check cross regions transition
      foreach (StateTransition(AtLeastStaticTo = to) as tr in StateTransitions)
      {
        match (to, tr.From)
        {
          | (null, _) => ()
          | (FsmState(PartOfRegion = Some(r), PartOfSubRegion = Some(sr)), FsmState(PartOfRegion = Some(r2), PartOfSubRegion = Some(sr2))) => 
              when (r : object == r2 && sr : object != sr2) AddError(FsmError.TransitionCrossOrthogonalBoundaryOfSameRegion(tr))
          | _         => ()
        }
      }
      
      CheckLocalTrans();
    }

    

  }
}
