// analyser.n
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Statechart;
using System.ComponentModel;

namespace Nemerle.Statechart
{

  using StateTransition;
  using ListUtils;

  [NotifyPropertyChange]
  partial class FsmAnalyser : INotifyPropertyChanged
  {

    CheckHistoryStates() : void
    {
      // check for duplicates
      foreach (FsmState(history = TwoOption.Two(n, n2)) in States)
      {
        match (n.type, n2.type)
        {
          | (HistoryType.Deep, HistoryType.Deep)
          | (HistoryType.Shallow, HistoryType.Shallow) => messager.AddError(FsmError.DuplicateHistory(n))
          | _                                          => ()
        }
      }
      
      // check history duplicates in state
      foreach (h in HistoryStates)
      {
        | PseudoStateNode.History(default = Some(d)) when (d.ToState == null) => messager.AddError(FsmError.DefaultTransitionMustGoToState(d, h))
        | _                                                                   => ()
      }
    }

    CheckActivities() : void 
    {
      def do_act = States.SelectMany(x => x.do_activity.Select(y => (x, y)));
      //def do_act = do_act.Filter((_, x) => x.Length != 0);

      def sm = do_act.Filter((x : FsmState, _) => x.PartOfStateMachine.HasValue);
      def do_act = sm.GroupBy((x : FsmState, _) => x.PartOfStateMachine.Value.SubMachineType);
      foreach (group in do_act)
      {
        foreach (st in group.GroupBy((x, _) => x.name))
        {
          def (need, not) = st.NToList().Partition((_, x) => x.need_completion);
          def st = FsmState(st.Key, this, StateType.State());
          if (need.Length > not.Length)
          {
            need.Iter((_, x) => x.state = st.name);
          } else
          {
            not.Iter((_, x) => x.state = st.name)
          }
        }
      }
    }
    
    
    public Analyse() : void
    {
      try
      {
        CheckParseErrors();
        CreateStateMap();
        CheckHistoryStates(); // check for errors in history
        CheckTransitions();
        CheckInitialTransitions();
        CreateTransitions();
        //CreateActiveStates();
        BreakIfError();
        //CreateTransitionPaths();
        TransformFsm();
        ResolveConflicts();
        MergeTransitions();
        CheckUsedElements();
        CheckActivities();
        CreateHistoryPaths(); // create history paths
        FinalizeAnalyse();
      }
      catch 
      {
        | x is FsmErrorException => 
          def err = x.error;
          when (err != null) messager.AddError(err);
      }
    }

  }
}
