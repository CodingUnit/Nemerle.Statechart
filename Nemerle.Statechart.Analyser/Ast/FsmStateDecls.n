using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  /// <summary>
  /// Description of FsmStateDecls.
  /// </summary>
  public partial class FsmState
  {

    public do_activity : list[StateAction.Do] {get;set;default []}

    [NotifyPropertyChange]
    public Super : FsmState {get;set;}
    
    public type : StateType;
    
    // calculated
    [Memoized(Super)]
    public super_states : list[FsmState] 
    {
      get
      {
        if (Super != null) 
        {
          Super.super_states.Append(Super).NToList()
        } else
        {
          []
        }
      }
    }
    
    [NotifyPropertyChange]
    public sub_states : list[FsmState] {get;set;default []}
    
    [Memoized]
    public initial_tran : option[StateTransition] 
    {
      get
      {
        initial.Map(_.tran)
      }
    }
    
    [Memoized]
    public initial : option[PseudoStateNode.Initial]
    {
      get
      {
        pseudo_states.FindType()
      }
    }
    
    [NotifyPropertyChange]
    public transitions : list[StateTransition] {get;set;default []}
    
    [NotifyPropertyChange]
    public pseudo_states : list[PseudoStateNode] {get;set;default []}
    
    public statemap : StateMap
    {
      get
      {
        analyser.statemap
      }
    }
    
    public state : StateNode.State;

    [Memoized(Super)]
    public hierarchy_pos : int 
    {
      get
      {
        if (Super != null) 
        {
          Super.hierarchy_pos + 1
        } else 0
      }
    }
    
    public entry : list[StateAction.Entry] {get;set;default []}
    public exit  : list[StateAction.Exit]  {get;set;default []}

    public copy_num : int {get;set;}
    
    public paths : TransitionPaths {get;set;}

    public static TopStateName : string = "[Top]";

    public name : string {get;set;}

    public analyser : FsmAnalyser;

    public Id : IdType {get;set;}

  }
}
