using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using SCG = System.Collections.Generic;

namespace Nemerle.Statechart
{
  using StateTransition;
  using PseudoStateNode;
  /// <summary>
  /// Description of FsmStateImpl.
  /// </summary>
  public partial variant FsmState
  {
    public override ToString() : string {Name}

    public virtual UpdateReference(an : FsmAnalyser) : FsmState * FsmAnalyser
    {
      def map = an.statemap;
      def (super, an) = if (super_state != null && super_state.reference) 
                        {
                          map.Get(super_state.Name, an)
                        } else (super_state, an);
      //def super = if (super.AstName == null) super.ChangeName(TopStateName) else super;
      def an = initial.FoldLeft(an, _.UpdateReference(_));
      def an = transitions.FoldLeft(an, _.UpdateReference(_));
      def an = history.Value.FoldLeft(an, _.UpdateReference(_));
      def sub_states = map.Get(sub_states);
      def (pseudo, an) = pseudo_states.MapThrough(an, _.UpdateReference(_));
      def (sub_states, an) = sub_states.MapThrough(an, _.UpdateReference(_));
      def st = ChangePseudoStatesSubStatesSuperStateAnalyser(pseudo, sub_states, super, an);
      (st, an)
    }

    //public FormatName() : void
    //{
    //  def name = name.SplitToList('.');
    //  this.name = name.Last();
    //}

    public DefineInitial(st : FsmState) : FsmState
    {
      def init = match (this)
                  {
                    | ConcurrentSubRegion(contained_in = r) => Initial(r, st)//initial ::= Initial(r, st)
                    | _ => Initial(this, st)//initial ::= Initial(this, st)
                  }
      ChangeInitial(init :: initial)
    }

    public AddSubState(st : FsmState) : FsmState
    {
      //st.analyser = Analyser;
      if (sub_states.Exists(x => x.Name == st.Name)) this else ChangeSubStates(sub_states + [st])//sub_states = sub_states + [st];
    }

    //public DefineTransition(tr : StateTransition) : void
    //{
    //  | Transition as tr => transitions ::= Transition(this, tr)
    //  | Internal   as tr => internal_tran ::= Internal(this, tr)
    //  | Initial    as tr => initial ::= tr
    //  | _ => ()
    //}

    //public SetCopy(ncopy : int) : FsmState
    //{
    //  def st = match (this)
    //           {
    //             | State               as st => State(st, false, ncopy)
    //             | ConcurrentRegion    as st => ConcurrentRegion(st, ncopy)
    //             | ConcurrentSubRegion as st => ConcurrentSubRegion(st, ncopy)
    //             | InlinedSubMachine   as st => InlinedSubMachine(st, ncopy)
    //             | SubMachine          as st => SubMachine(st, ncopy)
    //             | Final               as st => Final(st, false, ncopy)
    //           }

    //  //clones ::= st;
    //  st.ChangeCopyNumCloneFor(ncopy);
    //  //st.FindCopyName();
    //}

    //public clones : list[FsmState] = [];

    public DefineTransitions(tr : list[StateTransition]) : FsmState
    {
      def (tran, other)   = tr.PartitionType();
      def (intern, other) = other.PartitionType();
      def (init, _)       = other.PartitionType();
      ChangeInternalTranInitialTransitions(intern, init, tran)
          //  | Transition as tr => transitions ::= Transition(this, tr)
    //  | Internal   as tr => internal_tran ::= Internal(this, tr)
    //  | Initial    as tr => initial ::= tr

      //tr.Iter(DefineTransition)
    }

    //public DefineHistoryPath(st : string, nodes : HistoryPath) : void
    //{
    //  history_paths ::= (st, nodes)
    //}

    //public DefineHistoryPathRecurse(st : string, nodes : HistoryPath) : void
    //{
    //  def reg = this is ConcurrentRegion;
    //  when (Lived || reg) DefineHistoryPath(st, nodes);
    //  when (!reg) sub_states.Iter(x  => x.DefineHistoryPathRecurse(st, nodes))
    //}

    public DeleteTransitions(tr : list[StateTransition]) : FsmState
    {
      def (tran, other)     = tr.PartitionType();
      def (int_tran, other) = other.PartitionType();
      def (init, _)         = other.PartitionType();
      def initial           = initial.Exclude(init, (x, y) => StateTransition.FullComparer(x, y) == 0);
      def internal_tran     = internal_tran.Exclude(int_tran, (x, y) => StateTransition.FullComparer(x, y) == 0);
      def transitions       = transitions.Exclude(tran, (x, y) => StateTransition.FullComparer(x, y) == 0);
      ChangeInternalTranInitialTransitions(internal_tran, initial, transitions)
    }

    //public DeleteTransitionsNested(tr : list[StateTransition]) : void
    //{
    //  DeleteTransitions(tr);
    //  AllSubStates.Iter(x => x.DeleteTransitionsNested(tr));
    //}

    public TraverseTransitions(an : FsmAnalyser) : FsmState
    {
      def paths = TransitionPaths(this);
      paths.TraverseTransitions(an, true);
      ChangePaths(paths)
    }

    public this()
    {
    }

    public this(super : FsmState, name : string, reference : bool = false)
    {
      this();
      this.analyser = analyser;
      this.name = name;
      this.super_state = super;
      this.reference = reference
    }

    public this(st : StateNode.State, super : FsmState, analyser : FsmAnalyser, reference : bool = false)
    {
      this(st, super, reference);
      this.analyser = analyser;
    }

    public this(st : StateNode.State, super : FsmState, reference : bool = false)
    {
      this();
      state = st;
      name = st.name;
      this.reference = reference || state.reference;

      def st_from_node(n)
      {
        | StateNode.State(type = SubMachine) => SubMachine(n, this)
        | State(type = InlinedSubMachine)    => InlinedSubMachine(n, this)
        | _                                  => FsmState.State(n, this, false)
      }

      when (!reference)
      {
        do_activity = st.do_activity |> StateAction.Do(_.action.ActionList, this, _.flags);
        internal_tran = Internal.Create(this, st.internal_tran);
        entry = st.entry |> StateAction.Entry(_.action.ActionList, this, _.attrib);
        exit = st.exit |> StateAction.Exit(_.action.ActionList, this, _.attrib);
        history = StateHistory.Create(this, st.history);
        transitions = Transition.Create(this, st.transitions);
        initial = st.initial |> Initial(this, _);
        def (regs, subst) = st.sub_states.Partition(x => x.regions.HasValue);
        def regs = regs |> ConcurrentRegion(this, _, _.regions.Value); // TODO: сделать с помощью st_from_node
        super_state = super;
        def subst = (subst |> st_from_node(_)) + regs;
        sub_states = subst.Sort((x, y) => x.source_pos - y.source_pos);
        pseudo_states = st.pseudo_states |> PseudoStateNode.Create(_.node, this, null)
      }
    }    
    //public this(st : FsmState, reference : bool = false, ncopy : int = 0)
    //{
    //  this();
    //  this.reference = reference;
    //  name = st.name;
    //  analyser = st.analyser;
    //  do_activity = st.do_activity;
    //  internal_tran = if (ncopy != 0) st.internal_tran |> Clone(ncopy) else st.internal_tran;
    //  entry = st.entry |> Clone() :> StateAction.Entry;
    //  exit = st.exit |> Clone() :> StateAction.Exit;
    //  history =  if (ncopy != 0) st.history |> Clone(ncopy) else st.history;
    //  history.Iter(x => x.state = this);
    //  transitions = if (ncopy != 0) st.transitions |> Clone(ncopy) else st.transitions;
    //  initial = if (ncopy != 0) st.initial |> Clone(ncopy) else st.initial;
    //  super_state = st.super_state;
    //  sub_states = if (ncopy != 0) st.sub_states |> ChangeCopyNum(ncopy) else st.sub_states;
    //  sub_states.Iter(x => x.super_state = this);
    //  pseudo_states = if (ncopy != 0) st.pseudo_states |> SetCopy(ncopy) else st.pseudo_states
    //}

    public history_paths : list[string * HistoryPath] // paths from outer history
    {
      get
      {
        //def reg = this is ConcurrentRegion;
        //when (Lived || reg) DefineHistoryPath(st, nodes);
        //when (!reg) sub_states.Iter(x  => x.DefineHistoryPathRecurse(st, nodes))
        def shist = SuperStates.Filter(x => x.ParticipateInHistory.Exists(x => x.Type == HistoryType.Shallow));
        def spaths = shist |>< history_paths;
        def paths = ParticipateInHistory.Map(x => (x.state.name, HistoryPath(x.state, this, x)));
        paths.Iter((_, x) => x.CreatePath(analyser));
        paths + spaths
      }
    }

    public PathFromOut : StateTransition.Initial
    {
      get
      {
        def path = StateTransition.Initial(analyser.TopState, this);
        path.TraversePath(analyser);
        path
      }
    }

    ///// <summary>
    ///// Indicates what fsm lived in this state and it not only composite for others
    ///// </summary>
    //public Lived : bool 
    //{
    //  get
    //  {
    //    lived || clones.Exists(_.Lived)
    //  }
    //}

    public UsedInGenerate : bool
    {
      get
      {
        lived || AllLivedSubStates is _ :: _
      }
    }

    /// <summary>
    /// Hierarchy position
    /// </summary>
    public hierarchy_pos : int
    {
      get
      {
        SuperStates.Length
      }
    }

    public source_pos : int
    {
      get
      {
        state.pos
      }
    }

    public IsTopState : bool
    {
      get
      {
        Name == TopStateName
      }
    }




    CopyName : string
    {
      get
      {
        if (copy_num == 0) "" else $"$copy_num"
      }
    }

    public Name : string
    {
      get
      {
        match (this)
        {
          | Final                               => $"$(super_state)Final$CopyName"
          | State
          | ConcurrentRegion
          | ConcurrentSubRegion                 => NativeName
          | SubMachine(name = n)                => $"SubMachine$n$CopyName"
          | InlinedSubMachine(name = n)         => $"InlinedSubMachine$n$CopyName"
          | InlinedSubMachineInstance(name = n) => $"InlinedSubMachineInstance$n$CopyName"
        }
      }
    }

    public OriginalName : string
    {
      get
      {
        match (this)
        {
          | ConcurrentSubRegion(index = idx, contained_in = r) => $"$(r)SubRegion$idx"
          | _ => if (name != null) name else AstName
        }
      }
    }

    public NativeName : string
    {
      get
      {
        $"$OriginalName$CopyName"
      }
    }

    public join_completion_transitions : list[StateTransition]
    {
      get
      {
        completion_transitions.Filter(x => x.To is TransitionTarget.PseudoState(Join()))
      }
    }

    public AstName : string
    {
      get
      {
        state.name
      }
    }

    /// <summary>
    /// Events for which reacts state
    /// </summary>
    public events : list[StateEvent]
    {
      get
      {
        def evts = work_transitions |> Trigger;
        evts.RemoveDuplicatesSort(_.CompareTo(_))
      }
    }

    /// <summary>
    /// Events for which reacts state or nested states
    /// </summary>
    public events_nested : list[StateEvent]
    {
      get
      {
        def evts = events + (AllLivedSubStates |>< events);
        evts.RemoveDuplicatesSort(_.CompareTo(_));
      }
    }

    public SuperStatesInclude : list[FsmState]
    {
      get
      {
        this :: SuperStates
      }
    }

    public PartOfStateMachine : option[InlinedSubMachine]
    {
      get
      {
        SuperStatesInclude.FindType()[0]
      }
    }

    public AllHistory : list[StateHistory]
    {
      get
      {
        AllSubStates |>< history.Value
      }
    }

    public PartOfSubRegion : option[ConcurrentSubRegion]
    {
      get
      {
        SuperStatesInclude.FindType()[0]
      }
    }

    public PartOfSubRegions : list[ConcurrentSubRegion]
    {
      get
      {
        SuperStatesInclude.FilterType()
      }
    }

    public PartOfTopRegion : option[ConcurrentRegion]
    {
      get
      {
        match (PartOfRegions)
        {
          | [] => None()
          | lst => Some(lst.Last())
        }
      }
    }

    public PartOfRegion : option[ConcurrentRegion]
    {
      get
      {
        SuperStatesInclude.FindType()[0]
      }
    }

    public PartOfRegions : list[ConcurrentRegion]
    {
      get
      {
        SuperStatesInclude.FilterType()
      }
    }

    public GetPathFrom(st : FsmState) : list[FsmState]
    {
      def super = SuperStatesInclude.FirstElems(x => x.Name == st.Name);
      st :: super.Rev()
    }

    public PathName : list[FsmState]
    {
      get
      {
        (this :: SuperStates.SplitLast()[1]).Rev()
      }
    }

    public static NameComparer : FsmState * FsmState -> int = (x, y) => string.Compare(x.Name, y.Name);

    public PartOfRegionsOf(st : FsmState) : list[ConcurrentRegion]
    {
      SuperStatesInclude.FirstElems(_ == st).FilterType();
    }

    public RenameState() : void
    {
      entry.Iter(x => x.state = this);
      exit.Iter(x => x.state = this);
      //history.Map(x => x.state = this)
    }

    public FoldThis(
                state   : list[FsmState] * State -> FsmState,
                final   : Final            -> FsmState,
                sub_reg : list[FsmState] * ConcurrentSubRegion -> FsmState,
                region  : list[FsmState] * ConcurrentRegion -> FsmState,
                inlined_subm_inst : list[FsmState] * InlinedSubMachineInstance -> FsmState,
                sub_mach : SubMachine -> FsmState,
                inlined_fsm : list[FsmState] * InlinedSubMachine -> FsmState
              ) : FsmState
    {

      def xnode(n, sub : list[FsmState], ot)
      {
        match (n)
        {
          | null                            => ot
          | t when (sub != null)            => t.ChangeSubStatesT(sub)
          | x                               => x
        }
      }

      def stack = SCG.Stack();
      
      def pop(sub)
      {
        stack.Take(sub.Length)
      }
      
      def loop(st, _)
      {
        match (st)
        {
          | State(sub_states_t = sub) as t                        => def sub_acc = pop(sub); 
                                                                     def acc = xnode(state(sub_acc, t), sub_acc, t);
                                                                     stack.Push(acc);
                                                                     acc
          | Final as t                                            => def acc = xnode(final(t), null, t);
                                                                     stack.Push(acc);
                                                                     acc

          | ConcurrentSubRegion(sub_states_t = sub) as t          => def sub_acc = pop(sub); 
                                                                     def acc = xnode(sub_reg(sub_acc, t), sub_acc, t);
                                                                     stack.Push(acc);
                                                                     acc
                                                                                    
          | ConcurrentRegion(sub_regions_t = reg)  as t           => def regacc = pop(reg |> _ : FsmState);
                                                                     def acc = xnode(region(regacc, t), regacc, t);
                                                                     stack.Push(acc);
                                                                     acc
          | InlinedSubMachineInstance(sub_states_t = sub) as t    => def sub_acc = pop(sub); 
                                                                     def acc = xnode(inlined_subm_inst(sub_acc, t), sub_acc, t);
                                                                     stack.Push(acc);
                                                                     acc 
          | SubMachine as t                                       => sub_mach(t)
          | InlinedSubMachine(sub_states_t = sub) as t            => def sub_acc = pop(sub); 
                                                                     def acc = xnode(inlined_fsm(sub_acc, t), sub_acc, t);
                                                                     stack.Push(acc);
                                                                     acc

        }
      }

      Iterator.FoldLeft(null, loop)
    }
    
    public Fold[T](
                  init : T,
                  state   : T * State -> T,
                  final   : T * Final     -> T,
                  sub_reg : T * ConcurrentSubRegion -> T,
                  region  : T * ConcurrentRegion -> T,
                  inlined_subm_inst : T * InlinedSubMachineInstance -> T,
                  sub_mach : T * SubMachine -> T,
                  inlined_fsm : T * InlinedSubMachine -> T
                  ) : T
    {
      def loop(st, acc)
      {
        match (st)
        {
          | State as t                        => state(acc, t)
          | Final as t                        => final(acc, t)
          | ConcurrentSubRegion as t          => sub_reg(acc, t)
          | ConcurrentRegion  as t            => region(acc, t)
          | InlinedSubMachineInstance as t    => inlined_subm_inst(acc, t)
          | SubMachine as t                   => sub_mach(acc, t)
          | InlinedSubMachine as t            => inlined_fsm(acc, t)
        }
      }

      Iterator.FoldLeft(init, loop)
    }

    public LevelOrderIterator : IEnumerable[FsmState]
    {
      get
      {
       def queue = SCG.Queue();
       queue.Enqueue(this);
       
       def loop()
       {
         if (queue.IsEmpty()) () else
         {
           def node = queue.Dequeue();
           yield node;
           foreach (s in node.sub_states_t)
           {
             queue.Enqueue(s);
           }
           loop()
         }
       }
       
       loop()
      }
    }
    
    public Iterator : IEnumerable[FsmState]
    {
      get
      {
        def child = SCG.Stack();
        def parent = SCG.Stack();

        def loop_init()
        {
          if (child.IsEmpty()) () else 
          {
            def curr = child.Pop();
            parent.Push(curr);
            match (curr)
            {
              | FsmState where (sub_states_t = sub) => foreach (s in sub) child.Push(s);loop_init()
              | _                                   => loop_init()
            }
          }
        }

        child.Push(this);
        loop_init();
        parent
      }
    }
    //public UpdateFromTarget(target : TransitionTarget) : FsmState
    //{
    //  def update_pseudo(op, p)
    //  {
    //    | (Choice(name = on), Choice(name = n))
    //    | (Junction(name = on), Junction(name = n))
    //    | (Join(name = on), Join(name = n)) 
    //    | (Merge(name = on), Merge(name = n)) when (on == n)                     => p
    //    | (Terminate(state = on), Terminate(state = n)) when (on.Name == n.Name) => p
    //    | _                                                                      => op
    //  }

    //  match (target)
    //  {
    //    | History(h)       => ChangeHistory(history.Map(x => if (x == h) h else x))
    //    | Final(f)         => ChangeSubStates(sub_states.Map(x => if (f.Name == x.Name) f else x))
    //    | PseudoState(p)   => ChangePseudoStates(pseudo_states.Map(update_pseudo(_, p)))
    //    | _                => this
    //  }
    //}
  }
}
