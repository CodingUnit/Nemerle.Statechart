using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  using StateTransition;
  using ListUtils;
  using DecisionNode;  
  using GuardNode;
/// <summary>
/// Description of AnalyseTransitions.
/// </summary>
  public partial class FsmAnalyser
  {

    CheckInitialTransitions() : void
    {
      def check_auto_initial(st, sub)
      {
        match (st)
        {
          | FsmState.Final 
          | ConcurrentRegion => st
          | FsmState(initial = init) when init.IsEmpty() && !sub.IsEmpty() with head = sub.First() =>
              if (head is FsmState.Final) st else st.DefineInitial(head);
          | _ => null
        }
      }

      States.Iter(x=> check_duplicates(x.initial));  // check duplicates
      def top = if (FsmFlags.AutoInitial) TopState.ReduceSingle(check_auto_initial) else TopState;

      def top = if (top.initial.IsEmpty()) // if not have initial transition in top state
                  if (top.transitions.IsEmpty()) // if not have other transitions
                    check_auto_initial(top, top.sub_states) // initial is first substate
                  else top; // else initial transition is top state
                 else top;
      
      //def create_path_from_out(st, _)
      //{
      //  def path = StateTransition.Initial(TopState, st).Create();
      //  def path = path.TraversePath();
      //  st.ChangePathFromOut(path)
      //}
      
      //UpdateStateMap(top);
      //def top = top.ReduceSingle(create_path_from_out);
      UpdateStateMap(top);
    }

    is_crossed_orthogonal(tr : StateTransition, st : FsmState) : bool
    {
      | (StateTransition(To = TransitionTarget.State(state = FsmState(PartOfRegion = Some(r), PartOfSubRegion = Some(sr)))),
                        FsmState(PartOfRegion = Some(r2), PartOfSubRegion = Some(sr2))) when r.Num == r2.Num => sr2.Num != sr.Num
      | _ => false
    }

    CreateSuperStateTransitions() : void
    {
      def create_trans(st)
      {
        | FsmState(PartOfSubRegion = Some, sub_states = sub) with trans = st.all_transitions.Exclude(is_crossed_orthogonal(_, st))
        
        | FsmState(sub_states = sub, all_transitions = trans) => 

            def sub = sub.Map(x => x.DefineTransitions(trans.Map(t => t.ChangeSource(x).SetNewId())));
            st.ChangeSubStates(sub)
      }

      def states = TopState.LevelOrderMap(create_trans);
      statemap.Update(states)
    }

    CheckCrossTransitions() : void
    {
      def group(tr, st)
      {
        match (tr)
        {
          | _ :: []  => st // if not have group exit
          | StateTransition(Guard = Some()) as head :: tail  => 
              def (path, end) = tail.SplitFind(x => x.Guard.IsNone); // get all include last without GuardNode
              def tr = head.ChangeParentCrossed(path);
              def path = path.Map(_.ChangeCrossFor(true));
              def st = st.DeleteTransitions(end); // delete from usual transitions in state
              st.UpdateTransitions(tr :: path)
              
          | _ :: tail  => st.DeleteTransitions(tail)
          
          | _  => st
        }
      }

      def check_cross_tran(tr, st)
      {
        single_func
        {
          tr.NToList().Sort((x, y) => StateTransition.GuardComparer(y, x));
          __.Sort((x, y) => y.From.Updated.hierarchy_pos - x.From.Updated.hierarchy_pos); // sort on hierarchy position
          group(__, st); // group transitions
        }
      }

      def check_cross(st, _)
      {
        def tran = st.all_transitions_with_init;
        def group = FsmState.create_transition_group(tran).Where((_, x) => x.IsMoreThanOne()).Values();
        group.FoldLeft(st, check_cross_tran)
      }


      def top = TopState.ReduceSingle(check_cross); // check from the top state
      UpdateStateMap(top)
    }

    check_duplicates(tr : IEnumerable[StateTransition]) : void
    {
      def check_duplicate_tran(tr)
      {
        def check_begin_and_ends(tr : IEnumerable[_])
        {
          def equal(lst, lst2)
          {
            lst.Equals(lst2, _ == _);
          }

          when (tr.IsMoreThanOne())
          {
            def acts = tr.Select(x : StateTransition => x.ActionList);
            def res = acts.ElementsEqual(equal);
            when (res) AddError(FsmError.DuplicateTransition(tr));
          }
        }

        def ordinary = tr.Exclude(x => x is Initial || x.IsCompletion);
        def dups = ordinary.Duplicates(_.Guard); // find duplicates with same guards and same triggers
        if (!dups.IsEmpty())
        {
          AddError(FsmError.DuplicateTransition(dups))
        } else
        {
          def (intern, other) = tr.PartitionLazy(_ is Internal);
          def begin_and_end_eq = other.NToList().Group((x, y) => x.From.CompareTo(y.From) - x.To.CompareTo(y.To)).Select(x => x.AsEnumerable());
          def begin_and_end_eq = begin_and_end_eq.Where(_.IsMoreThanOne());
          def merged = begin_and_end_eq.Append(intern.Select(_ : StateTransition));
          merged.Iter(check_begin_and_ends)
        }
      }

      def check_override(tr)
      {
        match (tr.Find(x => x.Guard.IsNone))
        {
          | Some(t) => def tr = tr.Filter(_ != t : object);
              def warn = tr.Map(x => FsmWarning.TransitionOverriden(x, t));
              AddWarnings(warn)
          | _ => ()
        }
      }

      def group = FsmState.create_transition_group(tr).Values();
      def group = group.Where(_.IsMoreThanOne());
      group.Iter(check_duplicate_tran);
      group.Iter(check_override);
    }

  /// <summary>
  /// Check duplicates
  /// </summary>
    CheckDuplicates() : void
    {
      States.Iter(x => check_duplicates(x.this_transitions));
      States.Iter(x => check_duplicates(x.initial));
    }

    get_named(tar : TransitionTarget.Named) : TransitionTarget
    {
      match (tar)
      {
        | Named(name = n, transition = tr) =>

            def states = PseudoStates.Filter(x => x.NativeName.HasValue);
            match (states.Find(x => x.NativeName.Value == n))
            {
              | Some(t) => TransitionTarget.PseudoState(tr, this, t)
              | _       => AddError(FsmError.UnknownNode(tr));
                          tar
            }
      }
    }

    resolve_targets(st : FsmState, tr : TransitionTarget) : TransitionTarget
    {
      // TODO: может ли оно быть null?
      def tran = tr?.transition;

      match (tr)
      {
        | TransitionTarget.Final(state = null) as t =>
            def super = if (tran is Initial) st else st.Super;
            match (super.sub_states.FindType())
            {
              | Some(FsmState.Final as st)  => t.ChangeState(st)
              | _                           => tr
            }

        | PseudoState(node = Terminate(state = null) as node) =>
            match (PseudoStates.Filter(_ is PseudoStateNode.Terminate))
            {
              | []     => TransitionTarget.PseudoState(tran, this, node)
              | [term] => TransitionTarget.PseudoState(tran, this, term)
              | _      => AddError(FsmError.UnknownTerminateTransition(tran));
                          tr
            }

        | PseudoState(node = Join(state = null, name = n)) =>
            match (PseudoStates.FilterType())
            {
              | [] => AddError(FsmError.UnknownNode(tran));
                      null
              | joins =>
                  match (joins.Find(x : PseudoStateNode.Join => x.name == n))
                  {
                    | Some(j) => TransitionTarget.PseudoState(tran, this, j)
                    | _       => AddError(FsmError.UnknownNode(tran));
                                tr
                  }
            }

        | Named as t => def t = get_named(t);
                        resolve_targets(st, t)
        | Qualified(parent = st, target = t) => 
            def st = statemap.Get(st);
            match (st, t)
            {
              | (FsmState.SubMachine as m, TransitionTarget.State(state = FsmState(Name = n))) =>
                  def m = m.Scan(); // scan external sub machine for states
                  when (!m.ref_sub_states.Contains(n)) AddError(FsmError.SubMachineStateNotFound(m, n));
                  TransitionTarget.SubMachine(tran, this, m, n)
              | (null, _) => AddError(FsmError.UnknownState(t.transition.From, FsmState.State(st.Name), t.transition));
                            t
              | (st, t) => resolve_targets(st, t)
            }
        | t  => t
      }
    }

    create_join_transitions() : void
    {

      def check_join(j, tr : IEnumerable[_], atran)
      {
        match (j)
        {
          | PseudoStateNode.Join(state = st, target = t) =>

              if (tr.ElementsEqual((x, y) => x.Trigger == y.Trigger))
              {
                if (tr.All(x => x.From.PartOfSubRegion.HasValue) &&
                    tr.ElementsEqual((x, y) => x.From.PartOfRegion.Value == y.From.PartOfRegion.Value))
                {
                  def tran = StateTransition.Create(st, tr.First().Trigger, t, j);
                  def reg = tr.First().From.PartOfRegion.Value;
                  def tr = tr.OrderBy(x : StateTransition => x.FromRegion.index);
                  //j.joined_tran = tr;
                  //j.region = reg;

                  def j = j.ChangeRegionJoinedTranTransition(reg, tr, tran);
                  //j.transition = tran;
                  /// !!! mutable
                  def tr = tr.Map(x => x.ChangeTo(TransitionTarget.PseudoState(x, this, j))) : IEnumerable[_];
                  AddError(FsmError.JoinTransitionMustLeaveRegion(j, tr));
                  atran.Concat(tr)
                } else atran;
              } else 
              {
                AddError(FsmError.JoinTransitionMustHaveSameTrigger(j, tr));
                atran
              }
        }
      }

      def get_join(t, lst)
      {
        match (t)
        {
          | StateTransition(To = TransitionTarget.PseudoState(Join as j)) => (j, t) :: lst
          | _                                                             => lst
        }
      }

      def join_tran = States.SelectMany(x => x.all_transitions_with_init.FoldLeft([], get_join));
      def jtrans = join_tran.GroupBy(x => x[0].name);
      def jtrans = jtrans.Select(x => (x.First()[0], x.Values()));
      def trans= jtrans.FoldLeft(Enumerable.Empty(), ((x, y), a) => check_join(x, y, a));
      UpdateTransitions(trans)
    }

    create_fork_transitions() : void
    {
      def check_fork(tr, f, atrans)
      {
        match (f)
        {
          | PseudoStateNode.Fork(target = t) =>

              def st = t |> ToState;
              def reg = st |> PartOfSubRegions;
              if (!reg.IsEmpty() && reg.All(x => !x.IsEmpty()))
              {
                def sreg = reg |> Last();
                def dups = sreg.Duplicates((x, y) => x.index - y.index);
                if (dups.Length != 0) 
                {
                  AddError(FsmError.ForkMustGoToSeparateRegion(f));
                  atrans
                } else
                {
                  def reg = sreg.Head.contained_in;
                  def sub_trans = t |> StateTransition.Create(reg, null, _, f);
                  def sub_trans = sub_trans.Map(x => x.ChangeLocal(true));
              /// !!! mutable
                  def tr = tr.ChangeTo(TransitionTarget.PseudoState(tr, this, f.ChangeForkTran(sub_trans)));
                  tr :: atrans
                }
              } else
              {
                AddError(FsmError.ForkMustGoToRegion(f));
                atrans
              }
        }
      }

      def get_fork(t, lst)
      {
        match (t)
        {
          | StateTransition(To = TransitionTarget.PseudoState(Fork as f)) => (t, f) :: lst
          | _                                                             => lst
        }
      }

      def tran = States.SelectMany(x => x.all_transitions_with_init.FoldLeft([], get_fork));
      def tran = tran.FoldLeft([], ((x, y), a) => check_fork(x, y, a));
      UpdateTransitions(tran)
    }
  /// <summary>
  /// Creating Transitions
  /// </summary>
    CreateTransitions() : void
    {
      def check_targets(node, _)
      {
        match (node)
        {
          | TransitionTarget.PseudoState(node = PseudoStateNode.Junction as n) as node => node.ChangeNode(CheckJunctionChoice(n))
          | _                                                                          => null
        }

      }

      def check_tran(st, _)
      {
        def trans = st.all_transitions_with_init.Map(x => x.ChangeTo(x.To.Reduce(junction = check_targets)));
        st.UpdateTransitions(trans)
      }

      //States.Map(check_tran);
      UpdateStateMap(TopState.ReduceSingle(check_tran));
      create_join_transitions(); // creating join transitions
      create_fork_transitions(); // creating fork transitions
      CreateSuperStateTransitions(); // create parent transitions
      CheckDuplicates(); // check duplicates
      CheckCrossTransitions(); // look for cross transitions
    }

    CheckTransitions() : void
    {
      def proc_tran(st, tran)
      {
        match (tran.To)
        {
          | TransitionTarget.State(state = to) when (!statemap.Contains(st)) => AddError(FsmError.UnknownState(st, to, tran))
          | _                                                                => ()
        }
      }

      def check_cross_orthogonal(tr)
      {
        when (is_crossed_orthogonal(tr, tr.Source)) AddError(FsmError.TransitionCrossOrthogonalBoundaryOfSameRegion(tr));
      }

      def check_tran(st, _)
      {
        def st = st.TraverseTargets(resolve_targets(st, _), true); // first pass named and qualified
        st.TraverseTargets(resolve_targets(st, _)) // second pass final
      }

      foreach (st in States)
      {
        st.all_transitions_with_init.Iter(proc_tran(st, _));
        st.all_transitions.Iter(check_cross_orthogonal)
      }
      
      UpdateStateMap(TopState.ReduceSingle(check_tran))
    }

    RenameTransition(tran : IEnumerable[StateTransition]) : IEnumerable[string * StateTransition]
    {
      def (dups, tran) = tran.DuplicatesPartition(_.Name);
      def dups = dups.Map(x => x.ChangeNeedGuardedName(true));
      def dups = dups |> (GuardedName, _);
      def tran = tran |> (Name, _);
      def tran = tran.Concat(dups);
      def (dups, tran) = tran.DuplicatesPartition(x => x[0]);
      def dups = dups.GroupBy(x => x[0]);
      def err = dups.Map(x => FsmError.DuplicateTransition(x.Values()));
      AddErrors(err);
      tran
    }

    CreateTransitionPaths() : void
    {
      def traverse()
      {
        traverser = TransitionTraverser(this);
        traverser.Add(TopState.initial);
        traverser.Traverse(); // model the fsm traversing states and transition from top recursively through initial transition and pseudo states
        statemap.Update(traverser.CompletedStates);
        def tran = traverser.Completed;
        UpdateTransitions(tran);
      }

      def update_states()
      {
        def dest = trans_paths.SelectMany(x => x.Paths.Map((_, y) => y.Transition.Destination));
        //def tran = dest.SelectMany(_.Transition);
        //UpdateTransitions(tran);
        def hist = dest.OfType.[TransitionDestination.History]().Select(_.state);
        def hist = hist.Select(_.ChangeUsed(true));

        def update_hist(s, _)
        {
          def hist = hist.Where(x => x.state.Num == s.Num);
          hist.FoldLeft(s, (x, a) => a.UpdateHistory(x))
        }

        //statemap.Update(st);
        def top = TopState.ReduceSingle(update_hist);
        UpdateStateMap(top)
      }

      traverse(); // traverse paths
      update_states();
      CreateTransitionsWithPaths()
    }

    CreateTransitionsWithPaths() : void
    {
      def determine_used()
      {
        def empty = empty_states.Select(FsmWarning.StateNotUsed); // warning about all without substates and not lived that is not used
        def not_used = not_used_transitions.Select(FsmWarning.TransitionNotUsed);
        AddWarnings(empty);
        AddWarnings(not_used);
      }

      determine_used(); // determine used elements

      //def (used_hstates, not_used) = history_states.Partition(_.Used);
      //history_states = used_hstates;

      def trans = RenameTransition(used_transitions);
      def trans = Map(trans);
      def int_tran =  States.SelectMany(_.internal_tran);
      def int_tran =  RenameTransition(int_tran);
      def int_tran = Map(int_tran);
      def init = used_states_initial_tran;
      def init = RenameTransition(init);
      def init = Map(init);
      internal_transitions = int_tran;
      transitions = trans;
      initial_transitions = init;
      def hist = not_used_history.Select(FsmWarning.HistoryNotUsed);
      AddWarnings(hist)
    }

    UpdateInitial() : void
    {
      def warn = not_used_states_initial_tran.Select(FsmWarning.TransitionNotUsed);
      AddWarnings(warn)
    }

  }
}
