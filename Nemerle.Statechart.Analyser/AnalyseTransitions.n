using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  using StateTransition;
  using ListUtils;
  using DecisionNode;  
  using GuardNode;
/// <summary>
/// Description of AnalyseTransitions.
/// </summary>
  public partial class FsmAnalyser
  {

    CheckInitialTransitions() : FsmAnalyser
    {
      def check_auto_initial(st, sub)
      {
        match (st)
        {
          | (FsmState.Final) 
          | (ConcurrentRegion) => st
          | (FsmState where (Initial = [])) when (!sub.IsEmpty()) with head = sub.First() =>
              if (head is FsmState.Final) st else st.DefineInitial(head);
          | x => x
        }
      }

      def err = States.FoldLeft(ErrorInfo(), (x, a) => check_duplicates(x.Initial, a));  // check duplicates
      def top = if (FsmFlags.AutoInitial) TopState.ReduceSingle(check_auto_initial) else TopState;

      def top = if (top.Initial.IsEmpty()) // if not have initial transition in top state
                 if (top.transitions.IsEmpty()) // if not have other transitions
                   check_auto_initial(top, top.sub_states) // initial is first substate
                 else top; // else initial transition is top state
                else top;
      def map = statemap.Init(top);
      //def map = map.ChangeStates(states.ToArray());
      def an = AddErrors(err);
      an.ChangeStatemap(map)
    }

    is_crossed_orthogonal(tr : StateTransition, st : FsmState) : bool
    {
      | (StateTransition where (To = TransitionTarget.State(state = FsmState where (PartOfRegion = Some(r), PartOfSubRegion = Some(sr)))),
                        FsmState where (PartOfRegion = Some(r2), PartOfSubRegion = Some(sr2))) when r == r2 =>
          sr2 != sr
      | _ => false
    }

    CreateSuperStateTransitions() : FsmAnalyser
    {
      def create_trans(st)
      {
        | FsmState.State(super_state = null) => st
        | FsmState where (super_state = super, PartOfSubRegion = r) =>
            single_func
            {
              super.combined_transitions;
              if (r.HasValue) __.Where(x => !is_crossed_orthogonal(x, st)) else __;
              st.DefineTransitions(__)
            }
      }

      def states = States.Map(create_trans);
      ChangeStatemap(statemap.ChangeStates(states.ToArray()))
    }

    CheckCrossTransitions() : FsmAnalyser
    {
      def group(tr, st)
      {
        match (tr)
        {
          | _ :: []  => st // if not have group exit
          | StateTransition where (Guard = Some()) as head :: tail  => head.IsCrossed = true; // cross transition
              def (path, end) = tail.SplitFind(x  => x.Guard.IsNone); // get all include last without GuardNode
              head.ParentCrossed = path;
              path.Iter(x => x.CrossFor = true);
              st.DeleteTransitions(end); // delete from usual transitions in state
          | _ :: tail  => st.DeleteTransitions(tail)
          | _  => st
        }
      }

      def check_cross_tran(tr, st)
      {
        single_func
        {
          tr.NToList().Sort((x, y) => StateTransition.GuardComparer(y, x));
          __.Sort((x, y) => y.From.hierarchy_pos - x.From.hierarchy_pos); // sort on hierarchy position
          group(__, st); // group transitions
        }
      }

      def check_cross(st)
      {
        def tran = st.transitions.Concat(st.internal_tran).Concat(st.Initial);
        def group = FsmState.create_transition_group(tran).Where((_, x) => x.IsMoreThanOne()).Values();
        group.FoldLeft(st, check_cross_tran)
      }

      def states = States.Map(check_cross); // check from the top state
      ChangeStatemap(statemap.ChangeStates(states.ToArray()))
    }

    check_duplicates(tr : IEnumerable[StateTransition], a : ErrorInfo) : ErrorInfo
    {
      def check_duplicate_tran(tr, a)
      {
        def check_begin_and_ends(tr, a)
        {
          def equal(lst, lst2)
          {
            lst.Equals(lst2, _ == _);
          }

          if (tr.IsMoreThanOne())
          {
            def acts = tr.Select(x : StateTransition => x.ActionList);
            def res = acts.ElementsEqual(equal);
            if (res) a.AddError(FsmError.DuplicateTransition(tr)) else a;
          } else a;
        }

        def ordinary = tr.Exclude(x => x is Initial || x.IsCompletion);
        def dups = ordinary.Duplicates(x => x.Guard); // find duplicates with same guards and same triggers
        if (!dups.IsEmpty())
        {
          a.AddError(FsmError.DuplicateTransition(dups))
        } else
        {
          def (intern, other) = tr.Partition(_ is Internal);
          def begin_and_end_eq = other.NToList().Group((x, y) => x.From.CompareTo(y.From) - x.To.CompareTo(y.To)).Select(x => x.AsEnumerable());
          def begin_and_end_eq = begin_and_end_eq.Where(_.IsMoreThanOne());
          def merged = begin_and_end_eq.Append(intern.Select(_ : StateTransition));
          merged.FoldLeft(a, check_begin_and_ends)
        }
      }

      def check_override(tr, a)
      {
        match (tr.Find(x => x.Guard.IsNone))
        {
          | Some(t) => def tr = tr.Filter(_ != t : object);
              def warn = tr.Map(x => FsmWarning.TransitionOverriden(x, t));
              a.HeadAppend(warn)
          | _ => a
        }
      }

      def group = FsmState.create_transition_group(tr).Values();
      def group = group.Where(_.IsMoreThanOne());
      def dup = group.FoldLeft(a, check_duplicate_tran);
      def warn = group.FoldLeft([], check_override);
      dup.AddWarning(warn)
    }

  /// <summary>
  /// Check duplicates
  /// </summary>
    CheckDuplicates() : FsmAnalyser
    {
      def err = States.FoldLeft(ErrorInfo(), (x, a) => check_duplicates(x.this_transitions, a));
      def err = States.FoldLeft(err, (x, a) => check_duplicates(x.Initial, a));
      AddErrors(err)
    }

    get_named(tar : TransitionTarget.Named, an : FsmAnalyser) : TransitionTarget * FsmAnalyser
    {
      match (tar)
      {
        | Named(name = n, transition = tr) =>

            def states = PseudoStates.Filter(x => x.NativeName.HasValue);
            match (states.Find(x => x.NativeName.Value == n))
            {
              | Some(t) => (TransitionTarget.PseudoState(tr, t), an)
              | _ => (tar, an.AddErrors(FsmError.UnknownNode(tr)))
            }
      }   
    }

    check_targets(tr : TransitionTarget, an : FsmAnalyser) : TransitionTarget * FsmAnalyser
    {
      match (tr)
      {
        | PseudoState(node = Junction as j, transition = tr) as node => def (n, an) = CheckJunctionChoice(j, an);
            if (j != n : object) (TransitionTarget.PseudoState(tr, n), an) else (node, an)
        | _  => (tr, an)
      }

    }

    resolve_targets(st : FsmState, tr : TransitionTarget, an: FsmAnalyser) : TransitionTarget * FsmAnalyser
    {
      // TODO: может ли оно быть null?
      def tran = tr?.transition;

      match (tr)
      {
        | TransitionTarget.Final(state = null) as t =>
            def super = if (tran is Initial) st else st.super_state;
            match (super.sub_states.FindType())
            {
              | Some(st)  => (t.ChangeState(st), an)
              | _              => (t, an)
            }

        | PseudoState(node = Terminate(state = null) as node) as t =>
            match (PseudoStates.Filter(_ is PseudoStateNode.Terminate))
            {
              | [] => (TransitionTarget.PseudoState(tran, node), an)
              | [term] => (TransitionTarget.PseudoState(tran, term), an)
              | _ => def an = an.AddErrors(FsmError.UnknownTerminateTransition(tran));
                  (t, an)
            }

        | PseudoState(node = Join(state = null, name = n)) as t =>
            match (PseudoStates.FilterType())
            {
              | [] => def an = an.AddErrors(FsmError.UnknownNode(tran));
                  (t, an)
              | joins =>
                  match (joins.Find(x : PseudoStateNode.Join => x.name == n))
                  {
                    | Some(j) => (TransitionTarget.PseudoState(tran, j), an)
                    | _ => def an = an.AddErrors(FsmError.UnknownNode(tran));
                        (t, an)
                  }
            }

        | Named as t => def (t, an) = get_named(t, this);
            an.resolve_targets(st, t, an)
        | Qualified(parent = st, target = t) => 
            def (st, an) = an.statemap.Get(st, an);
            match (st, t)
            {
              | (FsmState.SubMachine as m, TransitionTarget.State(state = FsmState where (Name = n))) =>
                  def m = m.Scan(); // scan external sub machine for states
                  def an = if (m.ref_sub_states.Contains(n)) an else an.AddErrors(FsmError.SubMachineStateNotFound(m, n));
                  (TransitionTarget.SubMachine(tran, m, n), an)
              | (null, _) => def an = an.AddErrors(FsmError.UnknownState(t.transition.From, FsmState.State(st.Name), t.transition));
                  (t, an)
              | (st, t) => resolve_targets(st, t, an)
            }
        | t  => (t, an)
      }
    }

    create_join_transitions() : FsmAnalyser
    {

      def check_join(j, tr, err)
      {
        match (j)
        {
          | PseudoStateNode.Join(state = st, target = t) =>

              if (tr.ElementsEqual((x, y) => x.Trigger == y.Trigger))
              {
                if (tr.All(x => x.From.PartOfSubRegion.HasValue) &&
                    tr.ElementsEqual((x, y) => x.From.PartOfRegion.Value == y.From.PartOfRegion.Value))
                {
                  def tran = Transition(st, tr.Head.Trigger, t, j);
                  def reg = tr.Head.From.PartOfRegion.Value;
                  def tr = tr.Sort((x, y) => x.FromRegion.index - y.FromRegion.index);
                  //j.joined_tran = tr;
                  //j.region = reg;

                  def j = j.ChangeRegionJoinedTranTransition(reg, tr, tran);
                  //j.transition = tran;
                  /// !!! mutable
                  tr.Iter(x => x.To = TransitionTarget.PseudoState(x, j));
                  err
                } else FsmError.JoinTransitionMustLeaveRegion(j, tr) :: err
              } else FsmError.JoinTransitionMustHaveSameTrigger(j, tr) :: err
        }
      }

      def get_join(t, lst)
      {
        match (t)
        {
          | StateTransition where (To = TransitionTarget.PseudoState(Join as j)) => (j, t) :: lst
          | _                                                                    => lst
        }
      }

      def join_tran = States.FoldLeft([], (x, a) => x.all_transitions_with_init.FoldLeft(a, get_join));
      def jtrans = join_tran.Group((x, y) => x[0].name.CompareTo(y[0].name)).Map(x => (x.Head[0], x.Split()[1]));
      def err = jtrans.FoldLeft([], ((x, y), a) => check_join(x, y, a));
      AddErrors(err)
    }

    create_fork_transitions() : FsmAnalyser
    {
      def check_fork(tr, f, err)
      {
        match (f)
        {
          | PseudoStateNode.Fork(target = t) =>

              def st = t |> ToState;
              def reg = st |> PartOfSubRegions;
              if (!reg.IsEmpty() && reg.All(x => !x.IsEmpty()))
              {
                def sreg = reg |> Last();
                def dups = sreg.Duplicates((x, y) => x.index - y.index);
                if (dups.Length != 0) [FsmError.ForkMustGoToSeparateRegion(f)] else
                {
                  def reg = sreg.Head.contained_in;
                  def sub_trans = t |> StateTransition.Transition(reg, null, _, f) : StateTransition;
                  sub_trans.Iter(x => x.Local = true);
              /// !!! mutable
                  tr.To = TransitionTarget.PseudoState(tr, f.ChangeForkTran(sub_trans));
                  err
                }
              } else
              {
                FsmError.ForkMustGoToRegion(f) :: err
              }
        }
      }

      def get_fork(t, lst)
      {
        match (t)
        {
          | StateTransition where (To = TransitionTarget.PseudoState(Fork as f)) => (t, f) :: lst
          | _                                                                    => lst
        }
      }

      def tran = States.FoldLeft([], (x, a) => x.all_transitions_with_init.FoldLeft(a, get_fork));
      def err = tran.FoldLeft([], ((x, y), a) => check_fork(x, y, a));
      AddErrors(err)
    }
  /// <summary>
  /// Creating Transitions
  /// </summary>
    CreateTransitions() : FsmAnalyser
    {
      def check_tran(st, an)
      {
        st.all_transitions_with_init.FoldLeft(an, (x, a) => x.TargetTraverse(a, check_targets))
      }

      def an = States.FoldLeft(this, check_tran);
      def an = an.create_join_transitions(); // creating join transitions
      def an = an.create_fork_transitions(); // creating fork transitions
      def an = an.CreateSuperStateTransitions(); // create parent transitions
      def an = an.CheckDuplicates(); // check duplicates
      an.CheckCrossTransitions(); // look for cross transitions
    }

    CheckTransitions() : FsmAnalyser
    {
      def proc_tran(st, tran, err)
      {
        match (tran.To)
        {
          | TransitionTarget.State(state = to) when (!statemap.Contains(st.Name)) => FsmError.UnknownState(st, to, tran) :: err
          | _                                                                     => err
        }

      }

      def check_cross_orthogonal(tr, err)
      {
        if (is_crossed_orthogonal(tr, tr.Source)) FsmError.TransitionCrossOrthogonalBoundaryOfSameRegion(tr) :: err else err
      }

      def init(st, err)
      {
        def err = st.Initial.FoldLeft(err, proc_tran(st, _, _));
        st.all_transitions.FoldLeft(err, proc_tran(st, _, _));
      }

      def check_tran(st, an)
      {
        st.all_transitions_with_init.FoldLeft(an, (x, a) => x.TargetTraverse(a, (a, b) => resolve_targets(st, a, b)))
      }

      def err = States.FoldLeft([], init);
      def err = States.FoldLeft(err, (x, e) => x.all_transitions.FoldLeft(e, check_cross_orthogonal));
      def an = States.FoldLeft(this, check_tran);
      
      def update_states(an)
      {
        def check_target(t, a) 
        {
          t.To.Fold(a, state = (t, a) => t.state :: a, named = (t, a) => t.state :: a, junction_node = (t, a) => t.state :: a)
        }
        
        def st = States.FoldLeft([], (x, a) => x.all_transitions_with_init.FoldLeft(a, check_target));
        def map = an.statemap.CheckNew(st);
        ChangeStatemap(map)
      }
      
      def an = update_states(an);
      an.AddErrors(err)
    }

    RenameTransition(tran : IEnumerable[StateTransition], an : FsmAnalyser) : IEnumerable[string * StateTransition] * FsmAnalyser
    {
      def (dups, tran) = tran.DuplicatesPartition(x => x.Name);
      dups.Iter(x => x.NeedGuardedName = true);
      def dups = dups |> (GuardedName, _);
      def tran = tran |> (Name, _);
      def tran = tran + dups;
      def (dups, tran) = tran.DuplicatesPartition((x, y) => x[0].CompareTo(y[0]));
      def dups = dups.Group((x, y) => x[0].CompareTo(y[0]));
      def err = dups.Map(x => FsmError.DuplicateTransition(x.Split()[1]));
      def an = an.AddErrors(err);
      (tran, an)
    }

    CreateTransitionPaths() : FsmAnalyser
    {
      def traverse()
      {
        traverser.Add(TopState.Initial.NToList());
        def res = traverser.Traverse(this); // model the fsm traversing states and transition from top recursively through initial transition and pseudo states
        def map = statemap.CheckNew(res);
        ChangeStatemap(map)
      }

      def update_states(an)
      {
        def dest = an.trans_paths.FoldLeft(Enumerable.Empty(), (x, a) => a.Concat(x.Paths.Map((_, y) => y.Transition.Destination)));
        def st = dest.Select(_.ToState).Where(_ != null);
        def hist = dest.OfType.[TransitionDestination.History]().Select(_.state);
        def hist = hist.Select(x => x.ChangeUsed(true));
        
        def update_hist(s, _)
        {
          def hist = hist.Where(x => x.state.name == s.name);
          hist.FoldLeft(s, (x, a) => a.UpdateHistory(x))
        }
        
        def map = an.statemap.CheckNew(st);
        def top = map.Top.ReduceSingle(update_hist);
        
        an.statemap.Update(top)
      }

      def an = traverse(); // traverse paths
      def st = update_states(an);
      def an = an.ChangeStatemap(st);
      an.CreateTransitionsWithPaths()
    }

    CreateTransitionsWithPaths() : FsmAnalyser
    {
      def determine_used()
      {
        def empty = empty_states.Map(x => FsmWarning.StateNotUsed(x) : FsmWarning); // warning about all without substates and not lived that is not used
        def not_used = not_used_transitions.Map(x => FsmWarning.TransitionNotUsed(x) : FsmWarning);
        empty + not_used
      }
      
      def warnings = determine_used(); // determine used elements

      //def (used_hstates, not_used) = history_states.Partition(_.Used);
      //history_states = used_hstates;

      def (trans, an) = RenameTransition(used_transitions, this);
      def trans = Map(trans);
      def int_tran =  States |>< internal_tran;
      def (int_tran, an) =  an.RenameTransition(int_tran, an);
      def int_tran = Map(int_tran);
      def init = used_states_initial_tran;
      def (init, an) = an.RenameTransition(init, an);
      def init = Map(init);
      def an = an.ChangeInternalTransitionsTransitionsInitialTransitions(int_tran, trans, init);
      def hist = not_used_history.Map(x => FsmWarning.HistoryNotUsed(x) : FsmWarning);
      def warnings = warnings + hist;
      an.ChangeWarnings(GetWarnings(warnings))
    }
    
    UpdateInitial() : FsmAnalyser
    {
      def warn = not_used_states_initial_tran |> FsmWarning.TransitionNotUsed(_);
      AddWarnings(warn)
    }

  }
}
