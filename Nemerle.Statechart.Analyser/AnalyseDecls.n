using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Statechart;
using System;
using System.Collections.Generic;
using System.Linq;
using System.IO;
using System.Collections;

namespace Nemerle.Statechart
{
  using ListUtils;

  [Record]
  public partial class FsmAnalyser
  {

    [NeedChange(Warnings)]
    [NeedChange(Errors)]
    [NeedChange]
    public statemap : StateMap;

    public FsmFlags : MachineFlags
    {
      get
      {
        Fsm.flags
      }
    }

    //[Accessor] mutable states : list[FsmState];
    public Events : IEnumerable[string]
    {
      get
      {
        def evts = States.SelectMany(_.events);
        def evts = evts.Where(x => x.Name != null);
        def evts = evts.Select(_.Name);
        evts.Distinct()
      }
    }

    public mutable transitions : Map[string, StateTransition];

    //public events : IEnumerable[string] // event * for internal transition
    //{
    //  get
    //  {
    //    def int_evts =  
    //    def evts =  States |>< transitions |> Trigger;

    //    def int_tran_evt = int_evts.RemoveDuplicatesSort(_.CompareTo(_));
    //    //def tran_evt = evts.RemoveDuplicatesSort(_.CompareTo(_));
    //    //def int_tran_evt =  int_tran_evt.Exclude(tran_evt, _  ==  _);
    //    //int_tran_evt.Iter(x  => x.InternalOnly = true);
    //    //def evts =  (int_tran_evt + tran_evt) |> (Name, _);
    //    //Map(evts)
    //  }
    //}
    public trans_paths : IEnumerable[TransitionPaths]
    {
      get
      {
        States.Select(_.paths).Where(_ != null)
        //$[x.paths, x in States, x.paths != null]
      }
    }

    //public transition_nodes : Map[string, PathNode]
    //{
    //  get
    //  {
    //    def trans_nodes = transitions.Map((x, y) => (x, y.PathNode)); // nodes
    //    def trans_nodes = trans_nodes.RemoveDuplicatesSort(nodes_cmp);
    //    Map(trans_nodes)
    //  }
    //}

    //public int_trans_nodes : Map[string, PathNode]
    //{
    //  get
    //  {
    //    def int_nodes = internal_transitions |> (_, _[2].PathNode);
    //    Map(int_nodes.RemoveDuplicatesSort(nodes_cmp))
    //  }
    //}

    

    public actions : IEnumerable[string]
    {
      get
      {
        def acts =  trans_paths.SelectMany(_.Actions);
        def int_acts =  internal_transitions.Values().SelectMany(_.Actions);
        def acts = acts.Concat(int_acts);
        acts.Distinct()
        //acts.RemoveDuplicatesSort((x, y) => x.CompareTo(y));
        //def do_activities = States.FilterValues(_.do_activity);
      //def err = do_activities.FoldLeft([], check_do);
      }
    }

    public action_node : IEnumerable[PathNode]
    {
      get
      {
        all_transitions.SelectMany(_.PathNodeRecursive)
      }
    }

    public action_node_all : IEnumerable[PathNode]
    {
      get
      {
        def get_inline(p)
        {
          | PathNode.Entry(inline = inl)
          | Exit(inline = inl)           => inl.Map((x, _) => x)
          | _                            => Enumerable.Empty()
        }


        def inlined = action_node.SelectMany(get_inline);
        action_node.Concat(inlined)
      }
    }

    public not_used_history : list[StateHistory]
    {
      get
      {
        $[h, x in States, h in x.history.Value, !h.Used]
      }
    }

    public HistoryStates : IEnumerable[StateHistory]
    {
      get
      {
        States.SelectMany(x => x.history.Value).Where(_.Used)//$[h, x in States, h in x.history.Value, h.Used]//.Filter(x => x.history.HasValue) |>< history.Value
      }
    }

    public HistoryPaths : IEnumerable[HistoryPath]
    {
      get
      {
        def paths = States.SelectMany(x => x.HistoryPaths.Select(x => x[1]));
        def paths = paths.Exclude(_.IsDefault);
        paths.RemoveDuplicatesSort(_.Name)
      }
    }

    public work_transitions : Map[string, StateTransition]
    {
      get
      {
        def select = transitions.Filter((_, x)  => x.HistoryTarget == null);
        Map(select)
      }
    }

    //public work_transition_nodes : Map[string, PathNode]
    //{
    //  get
    //  {
    //    def work_nodes = work_transitions |> (_, _[2].PathNode);
    //    Map(work_nodes.RemoveDuplicatesSort(nodes_cmp))
    //  }
    //}

    //public initial_transition_nodes : Map[string, PathNode]
    //{
    //  get
    //  {
    //    def init_nodes = initial_transitions |> (_, _[2].PathNode);
    //    Map(init_nodes.RemoveDuplicatesSort(nodes_cmp))
    //  }
    //}

    states_initial_tran : IEnumerable[StateTransition]
    {
      get
      {
        States.SelectMany(_.initial)
        //States |>< Initial
      }
    }

    used_states_initial_tran : IEnumerable[StateTransition]
    {
      get
      {
        states_initial_tran.Where(_.Used)
      }
    }

    not_used_states_initial_tran : IEnumerable[StateTransition]
    {
      get
      {
        def not_used = states_initial_tran.Exclude(_.Used);
        def exc_used_clones = not_used.Exclude(x => x.clones.Exists(x => x.Updated.Used));
        exc_used_clones
      }
    }

    public mutable initial_transitions : Map[string, StateTransition];

    public DoActivities : IEnumerable[FsmState * StateAction.Do]
    {
      get
      {
        States.SelectMany(x => x.do_activity.Map(y => (x, y)))
      }
    }

    public Regions : IEnumerable[FsmState.ConcurrentRegion]
    {
      get
      {
        def regs = States.FilterType();
        regs.Filter(_.LivedNested);
      }
    }

    public all_transitions_with_init : IEnumerable[StateTransition]
    {
      get
      {
        if (TopState != null) TopState.all_transitions_nested else Enumerable.Empty()
      }
    }

    public not_used_regions : IEnumerable[FsmState.ConcurrentRegion]
    {
      get
      {
        def regs = States.FilterType();
        regs.Exclude(_.LivedNested);
      }
    }

    public PseudoStates : IEnumerable[PseudoStateNode]
    {
      get
      {
        States.SelectMany(_.pseudo_states)
      }
        //other_pseudo.Concat(junctions).Concat(choices).Concat(merges)
    }

    public other_pseudo : IEnumerable[PseudoStateNode]
    {
      get
      {
        PseudoStates.Exclude(x => x is PseudoStateNode.Junction || x is PseudoStateNode.Merge || x is PseudoStateNode.Fork)
      }
    }

    public junctions : IEnumerable[PseudoStateNode.Junction]
    {
      get
      {
        def get_junction(t, a)
        {
          match (t)
          {
            | StateTransition(To = TransitionTarget.PseudoState(Junction as j)) => j :: a
            | _                                                                 => a
          }
        }

        def junc = PseudoStates.FilterType();
        States.SelectMany(x => x.all_transitions_with_init.FoldLeft([], get_junction)).Concat(junc)
      }
    }

    public choices : IEnumerable[PseudoStateNode.Junction]
    {
      get
      {
        def get_choice(t, a)
        {
          match (t)
          {
            | StateTransition(To = TransitionTarget.PseudoState(Junction(type = JunctionType.Choice) as j)) => j :: a
            | _                                                                                             => a
          }
        }

        def choice = PseudoStates.FilterType();
        States.SelectMany(x => x.all_transitions_with_init.FoldLeft([], get_choice)).Concat(choice)
      }
    }

    public merges : IEnumerable[PseudoStateNode.Merge]
    {
      get
      {
        def get_merge(t, a)
        {
          match (t)
          {
            | StateTransition(To = TransitionTarget.PseudoState(Merge as j)) => j :: a
            | _                                                              => a
          }
        }

        def merge = PseudoStates.FilterType();
        States.SelectMany(x => x.all_transitions_with_init.FoldLeft([], get_merge)).Concat(merge)
      }
    }

    public forks : IEnumerable[PseudoStateNode.Fork]
    {
      get
      {
        def get_forks(t, a)
        {
          match (t)
          {
            | StateTransition(To = TransitionTarget.PseudoState(Fork as j)) => j :: a
            | _                                                             => a
          }
        }

        States.SelectMany(x => x.all_transitions_with_init.FoldLeft([], get_forks))
      }
    }

    [Accessor] mutable traverser : TransitionTraverser;

    public special_transitions : IEnumerable[StateTransition]
    {
      get
      {
        def get_special_transitions(tran, a)
        {
          match (tran)
          {
            | StateTransition.Transition(Destination = TransitionDestination.Fork(sub_tran = tr)) => a.HeadAppend(tr)
            | StateTransition.Transition(To = PseudoState(PseudoStateNode.Join(transition = tr))) => tr :: a 
            | _                                                                                   => a
          }
        }

        def special = used_transitions.FoldLeft([], get_special_transitions);
        special.RemoveDuplicatesSort(StateTransition.Comparer)
      //special_transitions.Iter(x => x.UpdateReference(statemap));
      }
    }

    public concurrent_states : IEnumerable[FsmState]
    {
      get
      {
        States.Where(_ is FsmState.ConcurrentSubRegion)
      }
    }

    public LivedStates : IEnumerable[FsmState]
    {
      get
      {
        States.Where(_.Lived)
      }
    }

    public empty_states : IEnumerable[FsmState]
    {
      get
      {
        def filter_used(st)
        {
          | FsmState.ConcurrentSubRegion
          | ConcurrentRegion
          | Final => false
          | _     => true
        }

        def empty =  States.Exclude(_.LivedNested); // determine lived states
        empty.Where(filter_used)
      }
    }

    public LivedNestedStates : IEnumerable[FsmState]
    {
      get
      {
        States.Filter(_.LivedNested) // determine lived states
      }
    }

    public ObjectsId : IEnumerable[IdObject]
    {
      get
      {
        States.SelectMany(_.ChildObjects).OrderBy(_.Num)
      }
    }

    public ObjectsIdDetail : IEnumerable[IdObject * int * FsmState]
    {
      get
      {
        ObjectsId.Select(x => (x, x.Num, statemap.ParentFor(x.Num)))
      }
    }

    public ChildsDetail : IEnumerable[FsmState * list[IdObject * int]]
    {
      get
      {
        States.Select(x => (x, x.ChildObjects.Map(x => (x, x.Num))))
        //ObjectsId.Select(x => (x, x.Num, statemap.ParentFor(x.Num)))
      }
    }
    
    [NeedChange(transitions, initial_transitions)]
    public mutable internal_transitions : Map[string, StateTransition];

    public States : IEnumerable[FsmState]
    {
      get
      {
        statemap.States
      }
    }

    //public PathsFromOut : IEnumerable[StateTransition]
    //{
    //  get
    //  {
    //    States.Select(_.PathFromOut)
    //  }
    //}

    public Fsm : StateNode.StateMachine;

    public NextJunctionNum() : int
    {
      unnamed_junction_num++;
      unnamed_junction_num
    }

    public NextChoiceNum() : int
    {
      unnamed_choice_num++; 
      unnamed_choice_num
    }


    CheckParseErrors() : void
    { 
      def check_err(err)
      {
        match (err)
        {
          | StateNode.Error(StateNode.DoActivity as node) => ParseError.DuplicateActivity(node)
          | StateNode.Error(node)                         => ParseError.UnknownElement(node)
        }
      }

      def flags = FsmFlags.errors.Select(x => ParseError.UnknownMachineFlag(x));// |> ParseError.UnknownMachineFlag(_);
      def err = Fsm.GetErrors().Select(check_err);
      def errs = err.Concat(flags);
      AddErrors(errs)
    }

    public AddParseError(err : ParseError) : string
    {
      "Parse Error:" + match (err)
      {
        | DuplicateActivity(act) => $<#Duplicate do activity $act, state may have only one do activity node#>
        | UnknownElement(node)   => $<#Unknown parse node $node #>
        | UnknownMachineFlag(f)  => $<#Unknown state machine flag "$f" #>
      }
      //Errors ::= str;
    }

    public BreakIfError() : void
    {
      when (!Errors.IsEmpty()) throw FsmErrorException()
    }

    public GetError(err : FsmError) : string
    {
      | StateNotFound(s)                                    => $<#State "$s" Not Found#>
      | SubMachineStateNotFound(m, s)                       => $<#Sub machine "$m" does not contain referenced sub state "$s"#>
      | UnknownTarget(tran, t)                              => $<#Transition "$tran" has unknown target "$t"#>
      | DuplicateTransition(tran)                           => $<#Duplicate transitions "$tran"#>;
      | DuplicateHistory(st, hist)                          => $<#Duplicate history of type "$(hist.type)" in "$st"#>
      | RegionMustContainInitial(reg, tran)                 => $<#Transition "$tran" requires region "$reg" to contain initial transition in each of it sub regions#>
      | SubRegionsMustContainInitial(reg, tran)             => $<#Transition "$tran" requires sub regions "$reg" to contain initial transitions#>
      | UnknownTerminateTransition(tran)                    => $<#Unknown terminate transition target of "$tran", there is several terminate pseudo states, specify full name#>                  
      | ForkMustGoToRegion(f)                               => $<#Fork "$f" must go to concurrent sub regions#>
      | ForkMustGoToSeparateRegion(f)                       => $<#Fork "$f" must go to different concurrent sub regions#>
      | UnknownNode(tran)                                   => $<#Unknown pseudostate node used in target of "$tran" transition #>
      | JoinTransitionMustHaveSameTrigger(j, tran)          => $<#Transitions "$tran" to join "$j" must have same event trigger#>
      | JoinTransitionMustLeaveRegion(j, tran)              => $<#Transitions "$tran" to join "$j" must leave sub state of same orthogonal region#>
      | TransitionCrossOrthogonalBoundaryOfSameRegion(tran) => $<#Transition "$tran" is cross orthogonal boundary of same region, this is not allowed#>
      | DefaultTransitionMustGoToState(tran, hist)          => $<#Default history transition "$tran" of history "$hist" must go to state#>
      | JunctionElseMustBeLast(j)                           => $<#Else node in junction "$(j.state).$j" must be the last alternative node#>
      | JunctionActionMaybeLast(j, a)                       => $<#Junction action "$a" in "$(j.state).$j" maybe single in node or last#>
      | JunctionElseMustAfterAnotherGuard(j)                => $<#Junction else node in "$(j.state).$j" must have preceded another GuardNode#>
      | EmptyJunction(j)                                    => $<#There is a empty node in junction "$(j.state).$j"#>
      | ChoiceElseMustBeLast(j)                             => $<#Else node in choice "$(j.state).$j" must be the last alternative node#>
      | ChoiceActionMaybeLast(j, a)                         => $<#Choice action "$a" in "$(j.state).$j" maybe single in node or last#>
      | ChoiceElseMustAfterAnotherGuard(j)                  => $<#Choice else node in "$(j.state).$j" must have preceded another GuardNode#>
      | EmptyChoice(j)                                      => $<#There is a empty node in choice "$(j.state).$j"#>
      | SeveralState(st)                                    => $<#There is several duplicate states of name $st #>
      | Sequence(e)                                         => $<#..$(e;"\n";GetError)#>
    }

    public GetWarning(err : FsmWarning) : string
    {
      match (err)
      {
        | HistoryNotUsed(hist = StateHistory(state = st, type = t))  => $<#"$t History in "$st" is not used in fsm life history#>
        | StateNotUsed(state = st)                                   => $<#State "$st" is not used in fsm life history#>
        | TransitionNotUsed(tran)                                    => $<#Transition "$tran" is not used in fsm life history#>
        | RegionNotUsed(reg)                                         => $<#Concurrent region "$reg" is not used in fsm life history#>
        | NoConcurrentActivity(act)                                  => $<#There is no other activities in do activity node $act. Flag is no meaning, concurrent activity means that several activities may run concurently in do activity region of state.#>
        | TransitionOverriden(tran,by)                               => $<#Transition $(tran.GuardedName) is overriden by $(by.GuardedName), due to execute priority rules, because these transition is unguarded#>
        | x                                                          => $"Unknown Warning $x"
      }
    }

    public AddWarning(err : FsmWarning) : void
    {
      def err = GetWarning(err);
      Warnings.Add(err);
    }

    public AddWarnings(err : IEnumerable[FsmWarning]) : void
    {
      def err = GetWarnings(err);
      Warnings.AddRange(err);
    }

    public AddErrors(err : IEnumerable[FsmError]) : void
    {
      def err = GetErrors(err);
      AddErrors(err)
    }

    public AddWarnings(err : IEnumerable[string]) : void
    {
      Warnings.AddRange(err);
    }

    public AddErrors(err : string) : void
    {
      Errors.Add(err)
    }

    public AddErrors(err : IEnumerable[string]) : void
    {
      Errors.AddRange(err)
    }

    public AddError(err : FsmError) : void
    {
      AddErrors(GetError(err))
    }

    public AddErrors(err : IEnumerable[ParseError]) : void
    {
      AddErrors(GetErrors(err))
    }

    public GetErrors(err : IEnumerable[FsmError]) : IEnumerable[string]
    {
      err.Select(GetError)
    }

    public GetErrors(err : IEnumerable[ParseError]) : IEnumerable[string]
    {
      err.Select(AddParseError)
    }

    public GetWarnings(err : IEnumerable[FsmWarning]) : IEnumerable[string]
    {
      err.Select(GetWarning)
    }

    public StatesId : StateId;

    public this(fsm : StateNode.StateMachine, name : string)
    {
      this.Fsm = fsm;
      Name = name;
      statemap = StateMap(this);
      StatesId = StateId(this);
    }

    public Name     : string;
    public Errors   : List[string] = List();
    public Warnings : List[string] = List();

    FinalizeAnalyse() : void
    {
      StatesId.CreateMasks();
      def longest = States.Max(x => x.Name.Length);
      def len = States.Count();
      def num_len = len.ToString().Length;
      
      
      def create_state_report(st)
      {
        | FsmState(Name = n, Id = null, Num = num) with id = IdType(BitArray(len))
        | FsmState(Name = n, Id = id, Num = num) => 
        
            def id_hex = id.Result.Fold("", (x, a) => x.ToString("X8") + a);
            def id_val = id.val.OfType.[bool]().MapListRev(x => if (x) '1' else '0');
            def id_val = string(id_val.ToArray());
            string.Format($<#{0, $longest}: Num = {1, $num_len}, Id = $(id_val) (0x$id_hex) #>, n, num)
      }
      
      
      when (FsmFlags.Debug) 
      {
        def info = TopState.LevelOrderIterator().Map(create_state_report);
        File.WriteAllLines($"$(Name)statemap.log", info);
      }
    }


    //public TransitionEvents : list[StateEvent]
    //{
    //  get
    //  {
    //    events_map.Values()
    //  }
    //}

    public Error : bool
    {
      get
      {
         !Errors.IsEmpty()
      }
    }

    public guards : IEnumerable[GuardNode]
    {
      get
      {
        def tran = transitions.Values().Concat(initial_transitions.Values());// |>< Guards;
        def guards = tran.SelectMany(_.Guards);
        //def init_guards =  |>< Guards;
        //def guards = guards + init_guards;
        guards.RemoveDuplicatesSort(_.Name)
      }
    }

    public history_transitions : Map[string, StateTransition]
    {
      get
      {
        transitions.Filter((_, x)  => x.HistoryTarget != null);
      }
    }

    public history_default_transitions : IEnumerable[StateTransition]
    {
      get
      {
        HistoryStates.FilterValues(_.default).Where(_.Used)//x.ActualTo != null)
      }
    }

    public used_transitions : IEnumerable[StateTransition]
    {
      get
      {
        states_transitions.Where(_.UsedInGenerate)
      }
    }

    public states_transitions : IEnumerable[StateTransition]
    {
      get
      {
        States.SelectMany(_.all_transitions)
      }
    }

    public not_used_transitions : IEnumerable[StateTransition]
    {
      get
      {
        def not_used = states_transitions.Exclude(_.UsedInGenerate);
        def not_used = not_used.Where(x => x.From.Lived);
        def not_used = not_used.Exclude(_.NotWork);
        not_used.Exclude(_.Used);
      }
    }

    public all_transitions : IEnumerable[StateTransition]
    {
      get
      {
        work_transitions.Values().Concat(initial_transitions.Values()).
          Concat(history_transitions.Values()).
          Concat(history_default_transitions).
          Concat(special_transitions)
          //Concat(PathsFromOut)
      }
    }

    //public ActionNodes : IEnumerable[PathNode]
    //{
    //  get
    //  {
    //    action_node//.RemoveDuplicatesSort(PathNode.Compare)
    //  }
    //}

    //public static StringComparator : string * string -> int = string.Compare(_, _, StringComparison.CurrentCulture);
    //public static StrEq : string * string -> bool = (x, y) => StringComparator(x, y) == 0;

    public TopState : FsmState
    {
      get
      {
        statemap.Top
      }
    }

  }
}
