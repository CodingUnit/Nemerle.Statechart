using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  [Record]
  class TraversePath
  {
    public Source : ActiveStateConfig;
    public Target : ActiveStateConfig;
    public tran_path   : TransitionPath;
    
    public this(source : ActiveStateConfig, target : ActiveStateConfig, tran_path : TransitionPath)
    {
      Source = source;
      Target = target;
      this.tran_path = tran_path;
    }
    
    Local : bool
    {
      get
      {
        tran_path.transition.Local
      }
    }
    
    [Memoized]
    SourceConfig : list[HState]
    {
      get
      {
        match (tran_source)
        {
          | HTarget.PseudoState(p) => p.state.active_config
          | State                  => Source.states
          | _                      => []
        }
      }
    }

    [Memoized]
    TargetConfig : list[HState]
    {
      get
      {
        match (tran_target)
        {
          | HTarget.PseudoState(p) => p.state.active_config
          | State                  => Target.states
          | _                      => []
        }
      }
    }
    
    [Memoized]
    MainSource : HState
    {
      get
      {
        match (tran_source)
        {
          | HTarget.PseudoState(p) => p.state
          | State(st)              => st
          | _                      => null
        }
      }
    }
    
    [Memoized]
    MainTarget : HState
    {
      get
      {
        match (tran_target)
        {
          | HTarget.PseudoState(null) => null
          | HTarget.PseudoState(p)    => p.state
          | State(st)                 => st
          | _                         => null
        }
      }
    }
    
    path : PathSequence = PathSequence();
    
    IsInternal : bool
    {
      get
      {
        transition.Target is HTarget.Nowhere
      }
    }
    
    transition : HTransition
    {
      get
      {
        tran_path.transition
      }
    }
    
    tran_source : HTarget
    {
      get
      {
        transition.Source
      }
    }
    
    tran_target : HTarget
    {
      get
      {
        transition.Target
      }
    }
    
    [Memoized]
    lca : HState
    {
      get
      {
        if (Local) MainSource.active_config.DupLast(MainTarget.active_config, (x, y) => x == y : object) else
        MainSource.active_super.DupLast(MainTarget.active_super, (x, y) => x == y : object)// TranTargetState
      }
    }
    
    public static FindPath(src_st : list[HState],
                           dest_st : list[HState],
                           exit : list[HState] = null,
                           entry : list[HState] = null) : list[HState] * list[HState]
    {
      def src = src_st ?? [];
      def dest = dest_st ?? [];
      def (ex, en) = src.RemoveDupFirst(dest, (x, y) => x == y : object);
      def exit  = exit ?? [];
      def entry = entry ?? [];
      (exit.Rev() + ex.Rev(), en + entry)
    }

    static GetEntryNode(st : HState) : IEnumerable[PathNode]
    {
      | HState(do_activity = do_act, entry = en) =>
              def act = en.Map(_.action);
              def en = act.Map(PathNode.Entry(st, _));
              def do_act = do_act.Select(PathNode.RunDoActivity(st, _));
              en.Concat(do_act)
    }

    static GetEntryPath(nodes : IEnumerable[HState]) : list[TranPath]
    {
      $[TranPath(st, GetEntryNode(st).NToList(), PathKind.Entry), st in nodes]
    }

    static GetActionPath(act : IEnumerable[FsmAction]) : list[TranPath]
    {
      $[TranPath(null, PathNode.Action(null, a) :: [], PathKind.Action), a in act]
    }

    static GetExitNode(st : HState) : IEnumerable[PathNode]
    {
      def do_act = st.do_activity.Map(x => PathNode.StopDoActivity(st, x));
      def nodes = if (st.history.HasValue) PathNode.SaveHistory(st, st.history.Value) :: do_act; else do_act;
      def act = st.exit.Map(_.action);
      def exit = act.Select(PathNode.Exit(st, _));
      nodes.Concat(exit)
    }

    public static GetExitPath(nodes : IEnumerable[HState]) : list[TranPath]
    {
      $[TranPath(st, GetExitNode(st).NToList(), PathKind.Exit), st in nodes]
    }

    //CreatePath(source : HState, target : HState) : PathSequence
    //{
    //  CreatePath(source.active_config, target.active_config)
    //}

    //CreatePath() : void
    //{
    //  match (tran_target)
    //  {
    //    | HTarget.State(st)                => CreatePath(Source.states, st.states)
    //    | HTarget.Nowhere                  => CreatePath([], [])
    //    | HTarget.PseudoState              => TraversePseudo()
    //    | _                                => null
    //  }
    //}

    //CreatePath(exit : IEnumerable[HState], act : IEnumerable[FsmAction], entry : IEnumerable[HState]) : PathSequence
    //{
    //  def exit = GetExitPath(exit); // create nodes for exit exit actions
    //  def act = GetActionPath(act); // add transition action
    //  def entry = GetEntryPath(entry); // create nodes for entry exit actions
    //  def path = PathSequence();
    //  path.AddExit(exit);
    //  path.AddAction(act);
    //  path.AddEntry(entry);
    //  path
    //}

    static GetPath(source : list[HState], target : list[HState], local : bool) : list[HState] * list[HState]
    {
      if (local) FindPath(source, target) else
      {
        def (source_last, source) = source.SplitLast();
        def (target_last, target) = target.SplitLast();
        FindPath(source, target, [source_last], [target_last])
      }
    }

    static GetPath(source : list[HState], target : list[HState], local : bool, act : list[FsmAction]) : PathSequence
    {
      def (ex, en) = GetPath(source, target, local);
      def exit = GetExitPath(ex);
      def entry = GetEntryPath(en);
      def act = GetActionPath(act);
      def path = PathSequence();
      path.AddEntry(entry);
      path.AddExit(exit);
      path.AddAction(act);
      path
    }
    
    AddPath(source : list[HState], target : list[HState], local : bool, act : list[FsmAction]) : void
    {
      def (ex, en) = GetPath(source, target, local);
      def exit = GetExitPath(ex);
      def entry = GetEntryPath(en);
      def act = GetActionPath(act);
      path.AddEntry(entry);
      path.AddExit(exit);
      path.AddAction(act);
    }
        
    //CreatePath(source : list[HState], target : list[HState]) : void
    //{

    //  def (exit, entry) = if (source.IsEmpty() && target.IsEmpty()) ([], []) else 
    //                       {
    //                         def st = (Transition.Source :> HTarget.State).state;
    //                         def targ = st.active_config;
    //                         def path = FindPath(source, targ)[0]; // exit to source transition vertex, but no include
    //                         def (exit, entry) = GetPath(targ, target, tran.Local); // exit according local or external transition rule from source and enter the target
    //                         (path + exit, entry) // exit to source vertex + source exit node and entry nodes to target
    //                       }
    //  CreatePath(exit, Transition.Actions, entry) // create path for transition
    //}

    CheckCompletion(act : ActiveStateConfig) : TargetDeterminator
    {
      def simple = act.simple_states;
      // TODO: final states in each region
      match (simple.Find(_.IsFinal))
      {
        | Some(final)  => match (final.active_super.SelectMany(x => x.transitions.Filter(x => x.IsCompletion)))
                          {
                            | []     => TargetDeterminator.State(act)
                            | _      => TargetDeterminator.Completion(act)
                          }
        | _            => def w_completion = simple.Filter(x => x.transitions.Exists(_.IsCompletion));
                          def w_do = w_completion.Filter(x => !x.do_activity.IsEmpty());
                          foreach (st in w_do)
                          {
                            st.do_activity.Iter(x => x.need_completion = true);
                          }
                          if (w_completion.IsEmpty()) TargetDeterminator.State(act) else TargetDeterminator.Completion(act)
      }
    }

    ExitPath(src : list[HState], dest : list[HState]) : void
    {
      def exit_path = GetPath(src, dest, true)[0];
      def nodes = GetExitPath(exit_path);
      path.AddExit(nodes)
    }
    
    AddExit(st : HState) : void
    {
      def nodes = GetExitPath(st :: []);
      path.AddExit(nodes)
    }
    
    AddEntry(st : HState) : void
    {
      def nodes = GetEntryPath(st :: []);
      path.AddEntry(nodes)
    }
    
    EntryPath(src : list[HState], dest : list[HState]) : void
    {
      def entry_path = GetPath(src, dest, true)[1];
      def nodes = GetEntryPath(entry_path);
      path.AddEntry(nodes)
    }
    
    ExitSource() : void
    {
      ExitPath(SourceConfig, lca.active_config); // exit from source config to transition source
      //AddExit(MainSource);
      //def target = MainSource.active_config.ChopLast().NToList();
      //ExitPath(target, lca.active_config); // exit from transition source to lca
    }
    
    Action() : void
    {
      def tran_path = GetActionPath(transition.Actions);
      path.AddAction(tran_path);
    }
    
    EntryTarget() : void
    {
      EntryPath(lca.active_config, TargetConfig);
      //def target = MainTarget.active_config.ChopLast().NToList();
      //EntryPath(lca.active_config, target);
      //AddEntry(MainTarget);
      //EntryPath(MainTarget.active_config, TargetConfig)
    }
    
    TraverseJunction(j : HPseudoState.Junction) : TargetDeterminator
    {
      def tree = j.decision_tree;
      def tree = TraverseDecisionTree(tree);
      j.decision_tree = tree;
      TargetDeterminator.Junction(tree, j.name)
    }
    
    //Clone() : TraversePath
    //{
    //  TraversePath(Source, Target, tran_path, path)
    //}
    
    TraverseDecisionTree(tree : DecisionNode) : DecisionNode
    {
      def tree = tree.Clone();
      foreach (j in tree.GetBreadthFirstEnumerable())
      {
        | DecisionNode.Target(target = tar) as t => 
             def target_conf = if (tar is HTarget.State(st)) tran_path.InitialConfiguration(st) else null;
             def p = TraversePath(Source, target_conf, tran_path);
             match (tar)
             {
               | HTarget.PseudoState(HPseudoState(state = st))
               | State(st)                                    => p.AddPath(t.state.active_config, st.active_config, true, j.actions)
               | _                                            => ()
             }
             t.path = p.path;
            //def p = TransitionPath(tran, recursive); // create path new path 
            //p.path = this.path.Clone(); // copy from this
            //def dest = tar.ToState; // if we know the target
            //p.DefPath(t.state, dest, true, j.actions); // create nodes to
            //def dest = p.TraverseTargetReg(tar, tran); // traversing target
            //t.path = p.path; // set node path to calculated
            //t.destination = dest; // destination set to dest
            def tar = p.ProcessTarget(tar);
            t.destination = tar;
        | _ => 
        
            def src = j.state;
            j.path = path.Clone(); // foreach nodes path from parent to childs
            foreach (n in j.ChildNodes)
            {
              def dest = n.state;
              def path = GetPath(src.active_config, dest.active_config, true, j.actions);
              n.path = path;
            }
      }
      tree
    }
    
    ProcessTarget(tar : HTarget) : TargetDeterminator
    {
      match (tar)
      {
        | HTarget.State                                   => CheckCompletion(Target)
        | HTarget.PseudoState(HPseudoState.Terminate)     => TargetDeterminator.Terminate()
        | HTarget.PseudoState(HPseudoState.History as h)  => TargetDeterminator.History(h)
        | HTarget.PseudoState(HPseudoState.Junction as j) => TraverseJunction(j)
        | _                                               => TargetDeterminator.NoStateChange()
      }
    }
    
    EmptyDecisionTarget(target : TargetDeterminator) : DecisionNode
    {
      DecisionNode.Target(transition.Actions, path, (transition.Source :> HTarget.State).state, transition.Target, target)
    }
    
    CreateDecisionTree(target : TargetDeterminator) : DecisionNode
    {
      | TargetDeterminator.Junction(tree, _)  => tree
      | _                                     => match (transition.Guard)
                                                {
                                                  | GuardNode.Empty      => EmptyDecisionTarget(target);
                                                  | GuardNode.Expr as g  => def target = EmptyDecisionTarget(target);
                                                                            DecisionNode.Condition(transition.Actions, path, (transition.Source :> HTarget.State).state, g, target, None())
                                                  | _                    => throw InvalidOperationException("GuardNode.Else is not permitted on this stage")
                                                }
    }

    //public InitSimpleDecisionTree(path : PathSequence) : void
    //{
    //  when (SimpleDecisionTree)
    //    foreach (n in DecisionTree.GetBreadthFirstEnumerable())
    //      {
    //        | DecisionNode.Target as t => t.destination = Destination;
    //                                  n.path = path;
    //        | _                        => n.path = path;
    //      }
    //}
    
    public CreateEntryPath() : Transition
    {
      Action();
      when (!IsInternal) EntryTarget();
      def target = ProcessTarget(tran_target);
      def tree = CreateDecisionTree(target);
      Transition(Source, null, transition.Trigger, transition.Guard, path, target, tree, 0)
    }
    
    public CreatePath() : Transition
    {
      if (IsInternal)
      {
        Action()
      } else
      {
        when (MainTarget != null)
        {
          ExitSource();
          Action();
          EntryTarget();
        }
      }
      def target = ProcessTarget(tran_target);
      def tree = CreateDecisionTree(target);
      def originate = if (tran_source is HTarget.State(st)) st else null;
      Transition(Source, originate, transition.Trigger, transition.Guard, path, target, tree, 0)
    }
    
    //TraversePseudo() : Transition
    //{
    //  match (pseudo)
    //  {
    //    | HPseudoState.History as h  => def st = pseudo.state;
    //                                    def path = CreatePath(tran, source.states, st.active_config);
    //                                    Transition(source, tran.Trigger, tran.Guard, path, TargetDeterminator.History(h))
                                        
    //    | _                          => null
    //  }
    //}
    
  }
}
