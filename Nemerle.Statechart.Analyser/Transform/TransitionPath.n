using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{

  /// <summary>
  /// Description of Config.
  /// </summary>
  class TransitionPath
  {
    public transition : HTransition;

    solver : ConfigSolver;

    active_proc : ActiveConfigProcessor;

    public this(tr : HTransition, solver : ConfigSolver, act_proc : ActiveConfigProcessor)
    {
      transition = tr;
      this.solver = solver;
      this.active_proc = act_proc;
    }

    Source : HTarget
    {
      get
      {
        transition.Source
      }
    }

    Target : HTarget
    {
      get
      {
        transition.Target
      }
    }

    SourceState : HState
    {
      get
      {
        (Source :> HTarget.State).state
      }
    }
    
    TargetState : HState
    {
      get
      {
        (Target :> HTarget.State).state
      }
    }
    
    Local : bool
    {
      get
      {
        transition.Local
      }
    }

    IsCompletion : bool
    {
      get
      {
        transition.IsCompletion
      }
    }

    public static GetSourceConfigs(tran : Transition, active_proc : ActiveConfigProcessor) : list[ActiveStateConfig]
    {
      def solver = ConfigSolver();
      def st = tran.OriginateFrom;
      match (st.hierarchy)
      {
              // simple state
        | []           => Solve(solver, active_proc, Config.Simple(st), _.states)
        | head :: []   => // sequential composite state
            def local = tran.Kind == TransitionKind.Local || tran.Kind == TransitionKind.Initial;
            if (!local && tran.Trigger.IsCompletion) // completion
            {
              def final = head.accept_state.Value;
              Solve(solver, active_proc, Config.Simple(final), _.states)
            } else
            {
              Solve(solver, active_proc, Config.Composite(st), _.states)
            }
        | _           => Solve(solver, active_proc, Config.Composite(st), _.states) // concurrent state
      }
    }
    
    get_source_configs(st : HState) : list[ActiveStateConfig]
    {
      match (st.hierarchy)
      {
              // simple state
        | []           => Solve(Config.Simple(st), _.states)
        | head :: []   => // sequential composite state
            if (!Local && IsCompletion) // completion
            {
              def final = head.accept_state.Value;
              Solve(Config.Simple(final), _.states)
            } else
            {
              Solve(Config.Composite(st), _.states)
            }
        | _           => Solve(solver, active_proc, Config.Composite(st), _.states) // concurrent state
      }
    }

    [Memoized]
    source_configs : list[ActiveStateConfig]
    {
      get
      {
        match (transition.Source)
        {
          | HTarget.State(st) => get_source_configs(st)
          | HTarget.PseudoState(p) =>
              match (p)
              {
                | HPseudoState.Join => []
                | _                 => []
              }
          | _ => []
        }
      }
    }

    public InitialConfiguration(st : HState) : ActiveStateConfig
    {
      def conf = Solve(Config.Unknown(st), x => x.init_state.Value :: []);
      conf.Head
    }

    //InitialConfiguration(st : list[HState]) : ActiveStateConfig
    //{
    //  def conf = Solve(Config.Concurrent(st), x => x.init_state.Value :: []);
    //  conf.Head
    //}
    static Solve(solver : ConfigSolver, proc : ActiveConfigProcessor, conf : Config, selector : HFsm -> list[HState]) : list[ActiveStateConfig]
    {
      solver.Init(conf, selector);
      def conf = solver.Solve();
      proc.CreateConfig(conf)
    }

    Solve(conf : Config, selector : HFsm -> list[HState]) : list[ActiveStateConfig]
    {
      solver.Init(conf, selector);
      def conf = solver.Solve();
      active_proc.CreateConfig(conf)
    }

    CreateTransitionPath(src_conf : ActiveStateConfig, target : ActiveStateConfig) : Transition
    {
      def trav = TraversePath(src_conf, target, this);
      trav.CreatePath()
    }
    
    TraversePseudo(source : ActiveStateConfig, _pseudo : HPseudoState) : Transition
    {
      def trav = TraversePath(source, null, this);
      trav.CreatePath()
    }
    
    public TraverseInitial() : Transition
    {
      def source = SourceState.active_config;
      def source = ActiveStateConfig([], source, [], [], null, false);
      def target = InitialConfiguration(TargetState);
      //CreateTransitionPath(init, source, target) // create nodes for entry exit actions
      def trav = TraversePath(source, target, this);
      trav.CreateEntryPath()
    }
    
    public Traverse() : list[Transition]
    {
      match (Target)
      {
        | HTarget.State(st)         => def target = InitialConfiguration(st);
                                       source_configs.Map(CreateTransitionPath(_, target))
        | HTarget.PseudoState(st)   => source_configs.Map(TraversePseudo(_, st))
        | HTarget.Nowhere           => source_configs.Map(CreateTransitionPath(_, null))
      }
    }
  }
}
