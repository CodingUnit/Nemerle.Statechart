namespace Nemerle.Statechart
{
  syntax module Transitions
  {
    using PrettyPrint;
    using Outline;
    using TokenNames;
    using StandardSpanClasses;
    using Whitespaces;
    using Identifiers;
    using Main;
    using Targets;
    using Actions;
    using Events;

    syntax Guard = '[' GuardDecl ']';

    regex ExtGoto   = "=>" | ")=>";
    regex LocalGoto = "(=>";
    regex AnyGoto   = ExtGoto | LocalGoto;

    syntax GuardExpr
    {
      | LongExpr = "${" (!"$}" Any)+ "$}"
      | Simple   = (!AfterTokens Any)+
      {
        regex AfterTokens = ']' | AnyGoto | '/';
      }
    }

    syntax GuardDecl
    {
      | Else = "else"
      | GuardExpr
    }

    syntax NormalTransition 
    {
      | LocalTransition    = Events Guard? ActionList? LocalGoto TransitionTarget;
      | InternalTransition = Events Guard? ActionList ';';
      | Transition         = Events Guard? ActionList? ExtGoto TransitionTarget;
    }

    syntax Transition
    {
      | NormalTransition
      //| RelocateTransition
    }

    //syntax A = "(=>";

  }
}
