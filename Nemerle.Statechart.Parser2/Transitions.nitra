namespace Nemerle.Statechart
{
  syntax module Transitions
  {
    using PrettyPrint;
    using Outline;
    using TokenNames;
    using StandardSpanClasses;
    using Whitespaces;
    using Identifiers;
    using Main;
    using Targets;
    using Actions;
    using Events;
    using Base;

    syntax Guard = '[' GuardDecl ']';

    regex ExtGoto   = "=>" | ")=>";
    regex LocalGoto = "(=>";
    regex AnyGoto   = ExtGoto | LocalGoto;

    syntax GuardExpr
    {
      | LongExpr = "${" (!"$}" Any)+ "$}"
      | Simple   = (!AfterTokens Any)+
        {
          regex AfterTokens = ']' | AnyGoto | '/';
        }
    }

    syntax GuardDecl
    {
      | Else = "else"
      | GuardExpr
    }

    alias PseudoTran = EntryPointTransition;

    syntax DefaultTransition = ActionList? "=>" SimpleTarget;

    syntax EntryPointTransition = ActionList? "=>" TransitionTarget;

    syntax TransitionPart = Guard? ActionList? "=>" TransitionTarget;

    syntax NormalTransition 
    {
      | LocalTransition    = Events Guard? ActionList? LocalGoto TransitionTarget;
      | InternalTransition = Events Guard? ActionList ';';
      | Transition         = Events Guard? ActionList? ExtGoto TransitionTarget;
    }

    syntax RelocateTransition
    {
      | Combined = '@' FullyQualified ':' '{' NormalTransition* '}'
      | Join     = JoinRelocateTran
      | State    = '@' FullyQualified ':' NormalTransition
      | Pseudo   = '@' FullyQualified ':' PseudoTran
    }

    syntax JoinRelocateTran
    {
      | Main = '@' '(' (FullyQualified; ',')+ ')' ':' Transition
      | Alt  = '@' FullyQualified '(' (Identifier; ',')+ ')' ':' Transition
    }

    syntax Transition
    {
      | NormalTransition
      | RelocateTransition
    }

  }
}
