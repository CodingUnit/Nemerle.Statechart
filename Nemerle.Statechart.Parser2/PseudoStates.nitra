namespace Nemerle.Statechart.Grammar
{
  syntax module PseudoStates
  {
    using PrettyPrint;
    using Outline;
    using TokenNames;
    using StandardSpanClasses;
    using Whitespaces;
    using Identifiers;
    using Targets;
    using Transitions;
    using Base;
    using Actions;
    
    syntax JunctionBody = '{' JuncTranBody+ '}'
    {
      syntax JuncTranBody = JunctionGuard? ActionList? "=>" TransitionTarget;
    }

    syntax History
    {
      | HistoryLong    = "history" ':' Type DefaultTransition? ';'
      {
        regex Type = "Deep" | "deep" | "Shallow" | "shallow";
      }
      | HistoryDeep    = "(H*)" (DefaultTransition ';')?
      | HistoryShallow = "(H)" (DefaultTransition ';')?
    }

    syntax EntryPoint = EntryPointHead EntryPointTransition
    {
      syntax EntryPointHead
      {
        | Ver1 = "entry:" Identifier 
        | Ver2 = Identifier ':'
        | Ver3 = '(' Identifier ')'
      }
    }
    
    alias ExitPointTransition = EntryPointTransition;

    syntax ExitPointDecl = ExitPointKey Identifier
    {
      regex ExitPointKey = "exit:" | "(X)" | "(x)";
    }

    syntax ExitPoint = ExitPointDecl ExitPointEnd
    {
      syntax ExitPointEnd 
      {
        | End = ";"
        | Tran = ExitPointTransition
      }
    }

    syntax PseudoState 
    {
      | Initial = InitialKeyword ActionList? "=>" InitialTarget
        {
          regex InitialKeyword = "initial" | "0";

          syntax InitialTarget 
          {
            | Simple   = SimpleTarget ';'
            | JunctionTarget
          }
        }
      | History
      | EntryPoint
      | ExitPoint
      | Terminate = TerminateKey ';'
        {
          regex TerminateKey = "terminate" | 'X' | 'x';
        }
      | Join     = "join"     Identifier TransitionPart
      | Junction = "junction" Identifier JunctionBody
      | Choice   = "choice"   Identifier '$'? JunctionBody
      | Merge    = "merge"    Identifier TransitionPart;
    }

    
  }
}
