namespace Nemerle.Statechart
{
  syntax module Events
  {
    using PrettyPrint;
    using Outline;
    using TokenNames;
    using StandardSpanClasses;
    using Whitespaces;
    using Identifiers;
    using Base;

    syntax EventList = '(' (Identifier; ',')* ')';

    syntax EventClass = "event" "class" FullyQualified EventList? ';';

    syntax Units = Identifier;

    syntax Parms = '(' (Identifier; ',')+  ')';

    syntax TimeExpr
    {
      | SimpleExpr = (FloatingNumber Units)+
      | FullTimeExpr = FullExpr
        {
          regex FullExpr = DaysFull | HoursMins | DaysWoMsec | MinsSec | Sec
          {
            regex DaysFull   = Days HoursMins Msec;
            regex HoursMins  = Integer ':' MinsSec;
            regex DaysWoMsec = Days HoursMins;
            regex MinsSec    = Integer ':' Sec;
            regex Sec        = Integer;

            regex Days       = Integer ('.' | ':');
            regex Msec       = ('.' | ',') Integer;
          }
        }
    }

    syntax Events = (SingleEvent; ',')+
    {
      syntax SingleEvent
      {
        | SimpleEvent = Identifier Parms?
        | TimedEvent  = "after" '(' TimeExpr ')'
      }
    }

    syntax DeferrableEvent
    {
      | Deferred = "deferred" '/' Events ';';
      | Deferrable = Events '/' "defer" ';';
    }
  }
}
