namespace Nemerle.Statechart.Grammar
{
  syntax module Events
  {
    using PrettyPrint;
    using Outline;
    using TokenNames;
    using StandardSpanClasses;
    using Whitespaces;
    using Identifiers;
    using Base;

    

    syntax EventClass = "event" "class" Name = FullyQualified List = EventList?  ';'
    {
      token EventList = '(' (Identifier; ',')* ')'; 
    }

    syntax SingleEvent
    {
      | Completion = CompKey
      {
        regex CompKey = "completion" | "_";
      }
      | SimpleEvent = Identifier Parms?
      {
        token Parms = '(' (Identifier; ',')+  ')';
      }
      | TimedEvent  = "after" '(' TimeExpr ')'
      {
        syntax TimeExpr
        {
          | SimpleExpr = (Num Units)+
          {
            syntax Num = FloatingNumber;

            syntax Units 
            {
             | Days = DaysRegex
             {
               regex DaysRegex = "days" | "d";
             }

             | Hours = HoursRegex
             {
               regex HoursRegex = "hours" | "h";
             }

             | Minutes = MinutesRegex
             {
               regex MinutesRegex = "minutes" | "min" | "m";
             }

             | Seconds = SecondsRegex
             {
               regex SecondsRegex = "seconds" | "secs" | "sec" | "s";
             }

             | MSec = MSecRegex
             {
               regex MSecRegex = "milliseconds" | "msec" | "ms";
             }

             | Ticks = TicksRegex
             {
               regex TicksRegex = "ticks" | "t";
             }
            }
          }
          | FullTimeExpr = FullExpr
            {
              regex FullExpr = Days? Hours ':' Mins (Sec Msec?)?
              {
                regex Days     = Integer '.';
                regex Hours    = Integer;
                regex Mins     = Integer;
                regex Sec      = ':' Integer;
                regex Msec     = '.' Integer;
              }
            }
        }
      }
    }

    syntax Events = (SingleEvent; ',')+;

    syntax SimpleEventList = (Identifier; ',')+;

    syntax DeferrableEvent
    {
      | Deferred = "deferred" '/' Events = SimpleEventList ';';
      | Deferrable = Events = SimpleEventList '/' "defer" ';';
    }

    syntax EventDecl = "event" Name = Identifier '(' Types = (Identifier; ',')+ ')' ';';
  }
}
