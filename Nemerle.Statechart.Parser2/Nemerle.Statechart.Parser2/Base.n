using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nitra;
using System.Globalization;

namespace Nemerle.Statechart
{
  using Nemerle.Statechart.Grammar;
  using Nemerle.Statechart.Grammar.MainAst;
  using Nemerle.Statechart.Grammar.BaseAst;
  using Nemerle.Statechart.Grammar.PseudoStatesAst;
  using Nemerle.Statechart.Grammar.TransitionsAst;
  using Nemerle.Statechart.Grammar.TargetsAst;
  using Nemerle.Statechart.Grammar.TransitionsAst.Guard;
  using Nemerle.Statechart.Grammar.MainAst.StateMachine.StateMachineDecl;
  using Nemerle.Statechart.Grammar.ActionsAst.ActionList.Action;

  /// <summary>
  /// Description of Base.
  /// </summary>
  partial public class Parser
  {
    public flags_map : Hashtable[string, MachineFlag] {get;set;}
    public env : GlobalEnv {get;set;}

    [Accessor]
    mutable result : Nitra.Internal.ParseResult;
    
    [Accessor]
    mutable source : SourceSnapshot;
    
    public Parse(loc : Nemerle.Compiler.Location, text : string) : StateNode.StateMachine * list[Error]
    {
      def parserHost = ParserHost();
      if (string.IsNullOrWhiteSpace(text)) (null, []) else
      {
        source = SourceSnapshot(text, loc.FileIndex, loc.File);
        result = Main.StateMachine(source, parserHost);
        def ast = MainAstWalkers.StateMachine(result);

        if (result.IsSuccess) 
        {
          match (ast)
          {
            | MainAst.StateMachine.Ast as a => def res = ConvertTree(a);
                                               (res, [])
            | _                             => (null, [])
          }

        } else (null, result.GetErrors().NToList())
        //    def (line, col) = error.Location.StartLineColumn;
        //    WriteLine($<#$line:$col: $(error.Message)#>);
      }

    }

    GetFloatNum(ast : Nitra.Ast) : double
    {
      def text = ast.GetText();
      mutable res;
      if (double.TryParse(text, NumberStyles.Float, CultureInfo.CreateSpecificCulture("en-US"), out res))
      {
        res
      } else
      {
        double.NaN
      }
      
    }
    
    GetLongNum(ast : Nitra.Ast) : option[long]
    {
      def text = ast.GetText();
      mutable res;
      if (long.TryParse(text, out res))
      {
        Some(res)
      } else
      {
        None()
      }      
    }
    
    GetNameIdentifier(fully : FullyQualifiedAny.Ast) : NameIdentifier
    {
      def id = fully.AnyIdentifiers[0];
      def pos = fully.Location.StartPos;
      match (id)
      {
        | AnyIdentifier.Ast :: []   => def text = $<#..$(id; ".")#>;
                                       NameIdentifier.Name(pos, text)
        | lst                       => def lst = GetIdentifierText(lst);
                                       def (last, lst) = lst.SplitLast();
                                       NameIdentifier.QualifiedName(pos, last, lst)
      }      
    }
    
    GetNameIdentifier(fully : FullyQualified.Ast) : NameIdentifier
    {
      def id = fully.QualifiedIdentifiers[0];
      def pos = fully.Location.StartPos;
      match (id)
      {
        | QualifiedIdentifier.Ast :: []   => def text = $<#..$(id; ".")#>;
                                             NameIdentifier.Name(pos, text)
        | lst                             => def lst = GetIdentifierText(lst);
                                             def (last, lst) = lst.SplitLast();
                                             NameIdentifier.QualifiedName(pos, last, lst)
      }

    }

    GetIdentifierText(id : list[EventIdentifier]) : list[string]
    {
      id.FoldRight([], (x, a) => if (x is EventIdentifier.Ast) x.GetText() :: a else a);
    }
    
    GetIdentifierText(id : list[QualifiedIdentifier]) : list[string]
    {
      id.FoldRight([], (x, a) => if (x is QualifiedIdentifier.Ast) x.GetText() :: a else a);
    }
    
    GetIdentifierText(id : list[AnyIdentifier]) : list[string]
    {
      id.FoldRight([], (x, a) => if (x is AnyIdentifier.Ast) x.GetText() :: a else a);
    }
    
    GetIdentifierData(id : list[AnyIdentifier]) : list[int * string]
    {
      id.FoldRight([], (x, a) => if (x is AnyIdentifier.Ast) (x.Location.StartPos, x.GetText()) :: a else a);
    }
    
    parse_expr(text : string) : PExpr
    {
      MainParser.ParseExpr(env, text);
    }
    
  }
}
