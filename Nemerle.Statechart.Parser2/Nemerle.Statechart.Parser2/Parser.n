using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nitra;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Statechart.Grammar;

namespace Nemerle.Statechart
{
  using Nemerle.Statechart.Grammar.MainParseTree;
  using Nemerle.Statechart.Grammar.BaseParseTree;
  using Nemerle.Statechart.Grammar.PseudoStatesParseTree;
  using Nemerle.Statechart.Grammar.TransitionsParseTree;
  using Nemerle.Statechart.Grammar.TargetsParseTree;
  using Nemerle.Statechart.Grammar.TransitionsParseTree.Guard;
  using Nemerle.Statechart.Grammar.MainParseTree.StateMachine.StateMachineDecl;
  using Nemerle.Statechart.Grammar.ActionsParseTree.ActionList.Action;
  using Nemerle.Statechart.Grammar.ActionsParseTree;
  /// <summary>
  /// Description of Parser.
  /// </summary>
  partial public class Parser
  {

    parse_action(pos : int, text : string) : Actions
    {
      def expr = parse_expr(text);
      def act = FsmAction.Expr(pos, expr);
      Actions(pos, act :: [])
    }

    GetActions(lst : ActionsParseTree.ActionList) : Actions
    {
      match (lst.Action)
      {
        | SimpleAction(SimpleAction = ActionsParseTree.SimpleAction.Empty)                                      => Actions(lst.Location.StartPos, [])
        | SimpleAction(SimpleAction = ActionsParseTree.SimpleAction.List(SData = (a, _))) => 
            def act = a.Map((x, _) => FsmAction.Normal(x.Location.StartPos, x.GetText(), []));
            Actions(lst.Location.StartPos, act)
                
        | SimpleExpr(SimpleExpr = ActionsParseTree.SimpleExpr(Expr = e))            
        | BraceExpr(BraceExpr = ActionsParseTree.BraceExpr(Exprs = e)) =>
            def text = e.GetText();
            parse_action(e.Location.StartPos, text);
        //| Expr(ActionsParseTree.ActionList.Action.Expr.Nested.Expr.BraceExpr(ActionsParseTree.BraceExpr.ParseTree(_, e, _))) => 
            
              
        | _ => Actions(lst.Location.StartPos, [])
      }
      //| _ => Actions(lst.Location.StartPos, [])
    }
    
    GetActions(lst : ActionsParseTree.ActList) : Actions
    {
      | Simple(ActionList = a) => GetActions(a)
      | BraceAction(BraceExpr = ActionsParseTree.BraceExpr(Exprs = e)) =>
          def text = e.GetText();
          parse_action(e.Location.StartPos, text);
      | _ => Actions(lst.Location.StartPos, [])
    }
    
    CreatePseudo(p : PseudoStatesParseTree.PseudoState) : option[StateNode.PseudoState]
    {
      def pos = p.Location.StartPos;
      def pseudo = match (p)
      {
        | PseudoState.Choice(AnyIdentifier = n, JunctionBody = b) => 
            def nodes = GetNodes(b);
            def name = n.GetText();
            PseudoStateModelNode.Junction(p.Location.StartPos, name, nodes, JunctionType.Choice)
            
        | PseudoState.EntryPoint(EntryPoint = PseudoStatesParseTree.EntryPoint(EntryPointHead = n, EntryPointTransition = tr)) => 
            match (GetEntryPointTran(tr))
            {
              | Some(tr) => 
                  match (n)
                  {
                    | PseudoStatesParseTree.EntryPoint.Nested.EntryPointHead.Ver1(AnyIdentifier = n)
                    | PseudoStatesParseTree.EntryPoint.Nested.EntryPointHead.Ver2(AnyIdentifier = n)
                    | PseudoStatesParseTree.EntryPoint.Nested.EntryPointHead.Ver3(AnyIdentifier = n) => 
                        def name = n.GetText();
                        PseudoStateModelNode.EntryPoint(pos, name, tr)
                    | _ => null
                  }
              | _ => null
                                
            }
        | PseudoState.ExitPoint(ExitPoint = PseudoStatesParseTree.ExitPoint(ExitPointDecl = PseudoStatesParseTree.ExitPointDecl(AnyIdentifier = n), ExitPointEnd = e)) => 
            def name = n.GetText();
            def tran = match (e)
            {
              | PseudoStatesParseTree.ExitPoint.Nested.ExitPointEnd.Tran(ExitPointTransition = tr) => GetEntryPointTran(tr)
              | _                                                                                  => None()
            }
            PseudoStateModelNode.ExitPoint(pos, name, tran)
            
        | PseudoState.History(PseudoStatesParseTree.History.HistoryDeep(_, t))    with type = HistoryType.Deep
        | PseudoState.History(PseudoStatesParseTree.History.HistoryShallow(_, t)) with type = HistoryType.Shallow => 
            def tran = match (t)
                       {
                         | Some(PseudoStatesParseTree.HistoryEnd.Tran(t, _)) => GetDefaultTran(t)
                         | _                                           => None()
                       }
            PseudoStateModelNode.History(pos, type, tran)
            
        | PseudoState.History(PseudoStatesParseTree.History.HistoryLong(_, _, type, t, _)) => 
            match (type)
            {
              | PseudoStatesParseTree.History.HistoryLong.Nested.Type.Deep    with type = HistoryType.Deep
              | PseudoStatesParseTree.History.HistoryLong.Nested.Type.Shallow with type = HistoryType.Shallow =>
                 def tran = match (t)
                      {
                        | Some(t) => GetDefaultTran(t)
                        | _       => None()
                      }
                 PseudoStateModelNode.History(pos, type, tran)
              | _                                                       => null
            }
            
        | PseudoState.Initial(_, g, a, _, t) => 
            def act = a.Map(GetActions);
            def guard = g.MapDefault(GetGuard, GuardNode.Empty(-1));
            def tar = match (t)
            {
              | PseudoState.Initial.Nested.InitialTarget.Simple(t, _)                                       => 
                 match (GetSimpleTarget(t))
                 {
                   | Some(t) => StateNode.Transition(pos, TargetType.NoTarget(-1), [], guard, act, t, TransitionKind.Local) :: []
                   | _       => []
                 }
               
               
              | PseudoState.Initial.Nested.InitialTarget.JunctionTarget(TargetsParseTree.JunctionTarget.ParseTree as t) => 
                 match (GetJunctionTarget(t, JunctionType.Junction))
                 {
                   | Some(TargetType.PseudoState(PseudoStateModelNode.Junction as j)) => 
                       j.nodes.Map(x => StateNode.Transition(x.pos, TargetType.NoTarget(-1), [], x.guard, x.action, x.to, TransitionKind.Local))
                   | _ => []
                 }
              | _ => []
            }
          
            match (tar)
            {
              | []  => null
              | lst => PseudoStateModelNode.Initial(pos, lst)
            }
        | PseudoState.Join(_, BaseParseTree.AnyIdentifier.ParseTree as n, TransitionsParseTree.TransitionPart.ParseTree(g, a, _, t)) => 
           def name = n.GetText();
           match (GetTran(p, None(), g, a, t, TransitionKind.External))
           {
             | Some(t) => PseudoStateModelNode.Join(pos, name, [], t)
             | _       => null
           }
           
        | PseudoState.Junction(_, n, PseudoStatesParseTree.JunctionBody.ParseTree as b) => 
           def name = n.GetText();
           def nodes = GetNodes(b);
           PseudoStateModelNode.Junction(pos, name, nodes, JunctionType.Junction)
           
        | PseudoState.Merge(_, n, TransitionsParseTree.TransitionPart.ParseTree(g, a, _, t)) =>
           def name = n.GetText();
           match (GetTran(p, None(), g, a, t, TransitionKind.External))
           {
             | Some(t) => def node = JunctionNode(pos, t.guard, t.action, t.to);
                          PseudoStateModelNode.Junction(pos, name, node :: [], JunctionType.Merge)
             | _       => null
           }
           
        | PseudoState.Terminate => PseudoStateModelNode.Terminate(pos)
        | _ => null
      }
      if (pseudo != null) Some(StateNode.PseudoState(pos, pseudo)) else None()
    }

    GetParent(par : MainParseTree.Parent) : option[string]
    {
      | MainParseTree.Parent.ParseTree(_, MainParseTree.Parent.ParentIdentifier.RegionReference(RegionsParseTree.RegionReference.ParseTree as r)) =>
          def parent = r.GetText();
          Some(parent)
      | MainParseTree.Parent.ParseTree(_, MainParseTree.Parent.ParentIdentifier.FullyQualified(BaseParseTree.FullyQualified.ParseTree((id, _)))) => Some($<#..$(id; ".")#>)
      | _ => None()
    }
    
    GetAttribute(attr : BaseParseTree.Attributes) : list[StateAttribute]
    {
      | BaseParseTree.Attributes.ParseTree(_, (a, _), _) => 
      
        def pos = attr.Location.StartPos;
        def check_attr(x)
        {
          if (x is BaseParseTree.Identifier.ParseTree as x) 
            match (x.GetText())
            {
              | "Initial" => StateAttribute.Initial(pos)
              | str       => StateAttribute.Unknown(pos, str)
            }
           else StateAttribute.Unknown(pos, null)
        }
        
        a.Map(check_attr)
      | _ => []
    }
    
    CreateState(st : StateDecl.State, decls : list[MainParseTree.StateDecl], type : StateType) : StateNode.State
    {
      match (st)
      {
        | StateDecl.State(a, _, n, p, _, d, _) as t => CreateState(t, n, p, a, decls ?? d, type)
      }
    }

    CreateState(ast : Nitra.ParseTree, name : BaseParseTree.AnyIdentifier, parent : option[MainParseTree.Parent], attr : option[BaseParseTree.Attributes], decls : list[MainParseTree.StateDecl], type : StateType, err : list[StateNode.Error] = null) : StateNode.State
    {
      def name = name.GetText();
      def parent = if (parent is Some(p)) GetParent(p) else None();
      def attr = attr.MapDefault(GetAttribute, []);
      CreateState(ast.Location.StartPos, attr, name, decls, type, parent, err);
    }
    
    CreateState(pos : int, attr : list[StateAttribute], name : string, decls : list[MainParseTree.StateDecl], type : StateType, parent : option[string], errors : list[StateNode.Error] = null) : StateNode.State
    {
      mutable defer  = [];
      mutable do_act = [];
      mutable entry  = [];
      mutable exit   = [];
      mutable pseudo = [];
      mutable states = [];
      mutable tran   = [];
      mutable err    = errors ?? [];
      def decls = decls.FoldBack([], (x, a) => if (x is MainParseTree.StateDecl.Sequence(h, t)) (h :: t) + a else x :: a);
      foreach (d in decls)
        {
          | StateDecl.DeferrableEvent(EventsParseTree.DeferrableEvent.Deferrable(EventsParseTree.SimpleEventList.ParseTree((e, _)), _, _, _))
          | StateDecl.DeferrableEvent(EventsParseTree.DeferrableEvent.Deferred(_, _, EventsParseTree.SimpleEventList.ParseTree((e, _)), _)) =>
              def lst = e.FoldBack([], (x, a) => if (x is EventIdentifier.ParseTree as x) TransitionEvent.Normal(x.Location.StartPos, x.GetText(), []) :: a else a);
              defer ::= StateNode.Defer(d.Location.StartPos, lst)
          | StateDecl.DoActivity(ActionsParseTree.DoActivity.ParseTree(ActList = a))                   => 
              def act = GetActions(a);
              do_act ::= StateNode.DoActivity(d.Location.StartPos, act, ActivityFlag.None);
          | StateDecl.Entry(ActionsParseTree.Entry.ParseTree(ActList = a)) => 
              def act = GetActions(a);
              entry ::= StateNode.Entry(d.Location.StartPos, ActionFlags.None, act)
          | StateDecl.Exit(ActionsParseTree.Exit.ParseTree(ActList = a)) => 
              def act = GetActions(a);
              exit ::= StateNode.Exit(d.Location.StartPos, ActionFlags.None, act)
          | StateDecl.PseudoState(p) => match (CreatePseudo(p))
                                        {
                                          | Some(p) => pseudo ::= p
                                          | _       => ()
                                        }
          | StateDecl.Regions(r) => match (CreateRegion(r))
                                    {
                                      | Some(st) => states ::= st
                                      | _        => ()
                                    }
          
          | StateDecl.State as t => 
              def state = if (t.StateDecls is StateDecl.RegionSequence as r :: []) 
               {
                 CreateRegionFromSequence(t, r); 
               } else
               {
                 CreateState(t, null, StateType.State())
               }
              states ::= state;
              
          | StateDecl.SubMachine(a, _, n, t, p, _, d, _) as st => 
             def type = t.MapDefault(x => if (x is StateDecl.SubMachine.Nested.TypeDecl.ParseTree(_, t, _)) t.GetText() else null, null);
             def state = CreateState(st, n, p, a, d, StateType.SubMachine(type, true));
             states ::= state;
             
          | StateDecl.Transition(TransitionsParseTree.Transition.NormalTransition(t)) => 
             match (CreateTransition(t))
             {
               | Some(t) => tran ::= t
               | _       => ()
             }
          | StateDecl.Transition(TransitionsParseTree.Transition.RelocateTransition as t) => 
             match (CreateRelocateTran(t))
             {
               | Some(t) => match (t)
                            {
                              | StateNode.Sequence(tr)     => def tr = tr.Map(x => x :> StateNode.Transition);
                                                              tran = tran + tr;   
                              | StateNode.Transition as tr => tran ::= tr
                              | _                          => () // others not supported yet
                            }
               | _       => ()
             }
          | _ => ()
        }
      StateNode.State(pos, name, parent, attr, entry.Rev(), exit.Rev(), tran.Rev(), do_act.Rev(), defer.Rev(), states.Rev(), pseudo.Rev(), type, err.Rev())
    }

    CreateRegion(reg : RegionsParseTree.Regions) : option[StateNode.State]
    {
      def get_separator_err(d)
      {
        def (region_sep, decls) = d.FoldBack(([], []), (x, (a1, a2)) => if (x is StateDecl.RegionSequence as x) (x :: a1, a2) else (a1, x :: a2));
        def sep = region_sep.Map(x => StateNode.Region(x.StateDecls.Head[0].StartPos));
        def err = sep.Map(x => StateNode.Error(x.pos, x));
        (err, decls)
      }
      
      match (reg)
      {
        | RegionsParseTree.Regions.Region(RegionsParseTree.OneRegion.ParseTree(a, _, n, p, _, d, _), and_regs) =>
            def pos = reg.Location.StartPos;
            def (err, decls) = get_separator_err(d);
            def state = CreateState(reg, n, p, a, decls, StateType.SubRegion());
            def (init, pseudo) = state.pseudo_states.Partition(x => x.node is PseudoStateModelNode.Initial);
            def reg = StateNode.State(pos, null, null, [], [], [], [], [], [], state.sub_states, init, StateType.SubRegion(), err);
            
          
            def create_and_regions(x, (a1, a2) as a)
            {
              match (x)
              {
                | RegionsParseTree.Regions.Region.Nested.AndSubRegion.ParseTree(tok, _, and_regs, _) => 
                   def (err, decls) = get_separator_err(and_regs);
                   def state = CreateState(tok.StartPos, [], null, decls, StateType.SubRegion(), None());
                   def st = StateNode.State(state.pos, null, state.parent, [], [], [], [], [], [], state.sub_states, state.pseudo_states.Filter(x => x is StateNode.PseudoState(PseudoStateModelNode.Initial)), state.type, state.errors + err);
                   (st :: a1, state :: a2)
                | _ => a
              }
            }
          
          
            def (and_regs, decls) = and_regs.FoldBack(([], []), create_and_regions);
          
            def decls = state :: decls;
            def states = reg :: and_regs;
          
            def en = decls.SelectMany(_.entry).NToList();
            def ex = decls.SelectMany(_.exit).NToList();
            def trans = decls.SelectMany(_.transitions).NToList();
            def defer = decls.SelectMany(_.defer).NToList();
            def do_act = decls.SelectMany(_.do_activity).NToList();
            def pseudo = decls.SelectMany(x => x.pseudo_states.Exclude(x => x is StateNode.PseudoState(PseudoStateModelNode.Initial))).Concat(pseudo).NToList();
            def err = decls.SelectMany(_.errors).NToList();
          
            def state = StateNode.State(pos, state.name, state.parent, state.attributes, en, ex, trans, do_act, defer, states, pseudo, StateType.Region(), err);
            Some(state)
          
           | RegionsParseTree.Regions.Empty(RegionsParseTree.OneRegion.ParseTree(a, _, n, p, _, d, _) as t) => 
               def (err, decls) = get_separator_err(d);
               def state = CreateState(t, n, p, a, decls, StateType.Region(), err);
               Some(state)
             
           | _ => None()
         }
    }
    
    CreateRegionFromSequence(st : StateDecl.State, seq : StateDecl.RegionSequence) : StateNode.State
    {
        def first = seq.StateDecl;
        def other = seq.StateDecls;
        def other = other.Map((_, x) => x);
        def lst = first :: other;
        
        def create_sub_regions(reg)
        {
          def st = CreateState(reg.Location.StartPos, [], null, reg :: [], StateType.SubRegion(), None());
          def trans = st.transitions;
          (trans, st.ChangeTransitions([]))
        }
        
        def (trans, regs) = lst.Map(create_sub_regions).Split();
        def trans = trans.Flatten();
        def st = CreateState(st, [], StateType.Region());//StateNode.State(first.Location.StartPos, )
        def st = st.ChangeTransitions(trans);
        def st = st.ChangeSubStates(regs);
        st
    }
    
  }
}
