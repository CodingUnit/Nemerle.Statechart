namespace Nemerle.Statechart
{
  syntax module Main
  {
    using PrettyPrint;
    using Outline;
    using TokenNames;
    using StandardSpanClasses;
    using Whitespaces;
    using Identifiers;
	  
    using PseudoStates;
    using Transitions;
    using Actions;
    using Regions;
    using Events;
    using Base;

	  syntax Parent = ':' ParentIdentifier
	  {
	    syntax ParentIdentifier 
	    {
		    | RegionReference 
		    | FullyQualified;
	    }
	  }

    syntax TypeDecl = '[' Identifier ']';

	  syntax StateDecl
	  {
	    | State = Attributes? "state" sm Identifier Parent? '{' StateDecl* '}'
      | Entry
      | Exit
      | SubMachine = Attributes? "submachine" Identifier TypeDecl? Parent? '{' StateDecl '}'
      | DoActivity
      | DeferrableEvent
      | PseudoState
      | Transition
      | RegionSequence = StateDecl ^ 20 (RegionDelimToken StateDecl ^ 20)+
      | Regions
      | Sequence =  StateDecl ^ 30 (StateDecl ^ 30)+
	  }
    
    token NameIdentifier 
	  {
		  | Identifier 
		  | CommaIdentifier = Start = "\"" Body = (!'\"' Any)+ End = "\"";
	  }
    
	  [StartRule, ExplicitSpaces]
	  syntax StateMachine = s StateMachineDecl* !Any
    {
      regex SyntaxDelim = ':' | '=';

      syntax StateMachineDecl
	    {
	      | Flags = "flags" SyntaxDelim? Identifier+ ';';
	      | Name  = "name" SyntaxDelim? NameIdentifier ';';
        | Using = "using" FullyQualified ';'
        | EventClass
        | EventDecl = "event" Identifier '(' (Identifier; ';')+ ')'
	      | StateDecl
	    }
    }

  }
}
