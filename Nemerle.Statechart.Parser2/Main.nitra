namespace Nemerle.Statechart.Grammar
{
  syntax module Main
  {
    using PrettyPrint;
    using Outline;
    using TokenNames;
    using StandardSpanClasses;
    using Whitespaces;
    using Identifiers;
	  
    using PseudoStates;
    using Transitions;
    using Actions;
    using Regions;
    using Events;
    using Base;

	  syntax Parent = ':' ParentIdentifier
	  {
	    syntax ParentIdentifier 
	    {
		    | RegionReference 
		    | FullyQualified;
	    }
	  }

	  syntax StateDecl
	  {
	    | State = Attributes? "state" sm Identifier Parent? '{' StateDecl* '}'
      | Entry
      | Exit
      | SubMachine = Attributes? "submachine" Identifier TypeDecl? Parent? '{' StateDecl* '}'
      {
        syntax TypeDecl = '[' Type ']'
        {
          syntax Type = Identifier;
        }
      }
      | DoActivity
      | DeferrableEvent
      | PseudoState
      | Transition
      | RegionSequence = StateDecl ^ 20 (RegionDelimToken StateDecl ^ 20)+
      | Regions
      | Sequence =  StateDecl ^ 30 (StateDecl ^ 30)+
	  }
    
	  [StartRule, ExplicitSpaces]
	  syntax StateMachine = s StateMachineDecl* !Any
    {
      regex SyntaxDelim = ':' | '=';

      syntax StateMachineDecl
	    {
	      | Flags = "flags" SyntaxDelim? Flags = (Identifier; FlagsDelim)+ ';'
        {
          token FlagsDelim
          {
            | Comma = ','
            | Spaces = s
          }
        }
	      | Name  = "name" SyntaxDelim? Name = NameIdentifier ';'
        {
          token NameIdentifier 
	        {
		        | Identifier 
		        | CommaIdentifier = "\"" Body "\""
            {
               token Body = (!'\"' Any)+;
            }
	        }
        }
        | Using = "using" Type = FullyQualified ';'
        | EventClass
        | EventDecl
	      | StateDecl
	    }
    }

  }
}
