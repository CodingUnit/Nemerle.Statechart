using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.ComponentModel;

namespace Nemerle.Statechart
{
  /// <summary>
  /// Description of Memoize.
  /// </summary>
  public class MemoizeCalc[T] : INotifyPropertyChanged
  {
    mutable cached_value : T;
    public Name : string;
    
    public Changed : bool {get;set;default true}

    public CalcFunc    : void -> T {get;set;}
    public CalcFuncExt : void -> void {get;set;}

    public this(name : string) { Name = name }
    //public this(func : void -> T) { CalcFunc = func }
    
    public Bind(obj : INotifyPropertyChanged, prop : string, func : void -> T) : void
    {
      Bind(obj, prop);
      CalcFunc = func;
    }
    
    public Bind(obj : INotifyPropertyChanged, prop : string) : void
    {
      def prop = prop.Trim();
      obj.ChangeBind(prop, Change)
    }

    public Bind(obj : INotifyPropertyChanged) : void
    {
      obj.ChangeBind(Change)
    }
    
    public Change() : void
    {
      Changed = true
    }

    Cached() : void
    {
      Changed = false;
      OnPropertyChanged()
    }
    
    public Value : T
    {
      get
      {
        when (Changed) 
        {
          if (CalcFunc != null) 
          {
            cached_value = CalcFunc();
            Cached();
          } else
            when (CalcFuncExt != null)
            {
              CalcFuncExt(); 
              Cached();
            }
        }
        cached_value
      }
      set
      {
        cached_value = value;
        Cached();
      }

    }
    
    OnPropertyChanged() : void
    {
      PropertyChanged?.Invoke(this, PropertyChangedEventArgs(Name))
    }
    
    public event PropertyChanged : PropertyChangedEventHandler;
    
  }
}
