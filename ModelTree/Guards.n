using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler;
using Nemerle.Extensions;

namespace Nemerle.Statechart
{
  

  [Record]
  public variant GuardNode : LocatedNode
  {
    | Empty   { this() {base(-1)} }
    | ParseString {expr : string;}
    | Expr 
    {
      expr : PExpr 
    }
    //| InState {state : string;}
    | Else

    public override ToString() : string {Name}

    public static Combine(a : GuardNode, b : GuardNode) : GuardNode
    {
      | (GuardNode.Empty, GuardNode.Empty) => GuardNode.Empty(a.pos)
      | (Expr(ae), Expr(be))   => GuardNode.Expr(a.pos, <[ $ae && $be ]>)
      | (Else, Else)           => GuardNode.Else(a.pos)
      | (_, a)                 => a
    }
    
    public Name : string
    {
      [Memoize]
      get
      {
        match (this)
        {
          | Expr(c)        => $"[$c]"
          | ParseString(s) => $"[$s]"
          | Else           => "[else]"
          | Empty          => ""
        }
      }
    }

    public IsEmpty : bool
    {
      [Memoize]
      get
      {
        this is Empty
      }
    }
    
    public NotEmpty : bool
    {
      [Memoize]
      get
      {
        !IsEmpty
      }
    }
    
    [RecordIgnore]
    public Env : GlobalEnv {get;set;}
    
    public static @+(a : GuardNode, b : GuardNode) : GuardNode
    {
      Combine(a, b)
    }
    
    public CompareTo(b : GuardNode) : int
    {
      Name.CompareTo(b.Name)
      //match (this, b)
      //{
      //  | (Expr(a), Expr(b))       => a.ToString().CompareTo(b.ToString())
      //  | (_, _)                   => -1
      //}
    }
    
  }
}
